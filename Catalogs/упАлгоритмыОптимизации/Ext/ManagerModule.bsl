#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


// Функция - Проверить корректность поставляемого алгоритма оптимизации
//
// Параметры:
//  АлгоритмОптимизации	 - СправочникСсылка.упАлгоритмыОптимизации	 - алгоритм.
// 
// Возвращаемое значение:
//  Булево - признак успешности проверки
//
Функция ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации(АлгоритмОптимизации) Экспорт
	
	Отказ = Ложь;
	Если Не АлгоритмОптимизации.ЗащищеннаяВерсия Тогда
		ТекстОшибки = НСтр("ru = 'Незащищенные версии поставляемых алгоритмов оптимизации не поддерживаются.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		ВнешняяОбработка 	= АлгоритмОптимизации.ВнешняяОбработка;
		ИмяВнешнейОбработки = "";
		Если Истина
			И ЗначениеЗаполнено(ВнешняяОбработка)
			И ЗначениеЗаполнено(ВнешняяОбработка.ТекущаяВерсия) 
			Тогда
			ДвоичныеДанные 			  = РаботаСФайламиСлужебныйВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(ВнешняяОбработка.ТекущаяВерсия).Получить();
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			ИмяВнешнейОбработки       = ВнешниеОбработки.Подключить(АдресВоВременномХранилище,, АлгоритмОптимизации.БезопасныйРежимВнешнейОбработки);
		Иначе
			ТекстОшибки = НСтр(СтрШаблон("ru = 'У подключаемого алгоритма оптимизации %1 не загружена внешняя обработка с произвольным алгоритмом.'", АлгоритмОптимизации)); 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецЕсли;	
	КонецЕсли; 
	
	Если Не Отказ Тогда
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВнешнейОбработки, АлгоритмОптимизации.БезопасныйРежимВнешнейОбработки);
		СведенияОбОбработке = Неопределено;
		Попытка
			СведенияОбОбработке = ВнешняяОбработка.СведенияОбОбработке();
		Исключение
			ТекстОшибки = НСтр("ru = 'В загруженной внешней обработке алгоритма оптимизации отсутствует обязательная экспортная функция СведенияОбОбработке().'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецПопытки;	
	КонецЕсли; 

	Если Не Отказ Тогда
		Если ТипЗнч(СведенияОбОбработке) = Тип("Структура") Тогда
			Если Не СведенияОбОбработке.Свойство("ИмяМакетаЗащиты") Тогда
				ТекстОшибки = НСтр("ru = 'У загруженной внешней обработки алгоритма оптимизации отсутствует обязательное свойство ""ИмяМакетаЗащиты"", возвращаемое вызовом функции СведенияОбОбработке().'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
			КонецЕсли;
			
			Если Не СведенияОбОбработке.Свойство("ИмяЗагруженнойВМакетОбработки") Тогда
				ТекстОшибки = НСтр("ru = 'У загруженной внешней обработки алгоритма оптимизации отсутствует обязательное свойство ""ИмяЗагруженнойВМакетОбработки"", возвращаемое вызовом функции СведенияОбОбработке().'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
			КонецЕсли;
		Иначе
			ТекстОшибки = НСтр("ru = 'У загруженной внешней обработки алгоритма оптимизации отсутствуют обязательные свойства, возвращаемые вызовом функции СведенияОбОбработке().'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецЕсли; 
	КонецЕсли; 

	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

#КонецЕсли
