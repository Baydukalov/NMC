
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упПравилаРасчета.СоздатьЭлемент();
	КонецЕсли;
	
	ЭтаФорма.ИмяФайлаВнешнейОбработки = Объект.ВнешняяОбработка.Наименование;
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ВнешняяОбработка) тогда
		ЭтаФорма.АдресФайла = ПолучитьНавигационнуюСсылку(Объект.ВнешняяОбработка, "ТекстХранилище");
	Иначе
		ЭтаФорма.ИмяФайлаВнешнейОбработки = "Файл не указан";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьКартинкуКомментария();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьДанныеФайлаВБазе(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидЗадачиОптимизацииПриИзменении(Элемент)
	
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеАлгоритмаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.ОписаниеАлгоритма", НСтр("ru = 'Описание алгоритма'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	УстановитьКартинкуКомментария();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ВыгрузитьВнешнююОбработку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		ТекстСообщения = Нстр("ru = 'Сначала нужно заполнить наименование'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Наименование");
		Возврат;
	КонецЕсли; 
	Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма, Новый Структура("ИмяШаблона", ""));
	ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВнешнююОбработку(Команда)
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗавершенияПолученияФайла", ЭтаФорма);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении,, ИмяФайлаВнешнейОбработки, Истина, УникальныйИдентификатор); 

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьКартинкуКомментария()
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступность()
	
	Если Объект.ВидЗадачиОптимизации = ПредопределенноеЗначение("Перечисление.упВидыЗадачОптимизации.ЗадачаКоммивояжера") Тогда
		Элементы.ГруппаСтраницыНастройки.ТекущаяСтраница = Элементы.ГруппаСтраницаТиповыеНастройкиЗК;
	Иначе	
		Элементы.ГруппаСтраницыНастройки.ТекущаяСтраница = Элементы.ГруппаСтраницаТиповыеНастройкиЗМТ;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после загрузки файла.
//
Процедура ОбработкаЗавершенияПолученияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		Если Не ПроверитьИнтерфейсВнешнейОбработкиНаСервере(Адрес) Тогда
			Возврат;
		КонецЕсли; 
		ОбновитьДоступность();
		сткИмяФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
		ЭтаФорма.ИмяФайлаВнешнейОбработки = сткИмяФайла.Имя;
		ЭтаФорма.АдресФайла = Адрес;
		ЭтаФорма.Модифицированность = Истина;
		ТекстСообщения = Нстр("ru = 'Внешняя обработка загружена: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыбранноеИмяФайла);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьИнтерфейсВнешнейОбработкиНаСервере(Адрес)
	
	СведенияОбОбработке = Неопределено;
	Попытка
		ИмяВнешнейОбработки = ВнешниеОбработки.Подключить(Адрес,, Объект.БезопасныйРежимВнешнейОбработки);
		ВнешняяОбработкаПроверки = ВнешниеОбработки.Создать(ИмяВнешнейОбработки, Объект.БезопасныйРежимВнешнейОбработки);
	Исключение
		ТекстСообщения = Нстр("ru = 'Ошибка компиляции указаной внешней обработки: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецПопытки;
	
	Попытка
		СведенияОбОбработке = ВнешняяОбработкаПроверки.СведенияОбОбработке();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru = 'В указанной внешней обработке отсутствует обязательная экспортная функция СведенияОбОбработке()'"));
		Возврат Ложь;
	КонецПопытки;
	
	Если СведенияОбОбработке <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, СведенияОбОбработке);
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеФайлаВБазе(Знач ТекущийОбъект)
	
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		Если Не ТекущийОбъект.ВнешняяОбработка.Пустая() Тогда
			НовыйФайл = ТекущийОбъект.ВнешняяОбработка.ПолучитьОбъект();
		Иначе
			НовыйФайл = Справочники.Файлы.СоздатьЭлемент();
		КонецЕсли;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Попытка
			СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИмяФайлаВнешнейОбработки);
			
			НовыйФайл.ВладелецФайла = Справочники.ПапкиФайлов.АлгоритмыОптимизации;
			НовыйФайл.ТекстХранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФайла));
			НовыйФайл.Наименование = СтруктураИмениФайла.ИмяБезРасширения;
			НовыйФайл.Описание = ИмяФайлаВнешнейОбработки;
			НовыйФайл.ПолноеНаименование = СтруктураИмениФайла.ИмяБезРасширения;
			НовыйФайл.ТекущаяВерсияДатаМодификацииФайла = ТекущаяУниверсальнаяДата();
			НовыйФайл.Автор = Пользователи.ТекущийПользователь();
			НовыйФайл.ХранитьВерсии = Ложь;
			НовыйФайл.Записать();
			
			СведенияОФайле 		= РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
			СведенияОФайле.ИмяБезРасширения 			= СтруктураИмениФайла.ИмяБезРасширения;
			СведенияОФайле.РасширениеБезТочки 			= ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураИмениФайла.Расширение);
			СведенияОФайле.ХранитьВерсии 				= НовыйФайл.ХранитьВерсии;
			СведенияОФайле.ВремяИзмененияУниверсальное 	= НовыйФайл.ТекущаяВерсияДатаМодификацииФайла;
			СведенияОФайле.АдресВременногоХранилищаФайла = АдресФайла;
			Версия = РаботаСФайламиСлужебныйВызовСервера.СоздатьВерсию(НовыйФайл.Ссылка, СведенияОФайле);
			РаботаСФайламиСлужебныйВызовСервера.ОбновитьВерсиюВФайле(НовыйФайл.Ссылка, Версия, СведенияОФайле.АдресВременногоХранилищаФайла);
			ЗафиксироватьТранзакцию();
			УдалитьИзВременногоХранилища(СведенияОФайле.АдресВременногоХранилищаФайла);
		Исключение
			ОтменитьТранзакцию();
			ОписаниеОшибки = ОписаниеОшибки();
			ЗаписьЖурналаРегистрации("Ошибка записи", УровеньЖурналаРегистрации.Ошибка,, НовыйФайл.Ссылка, ОписаниеОшибки);
		КонецПопытки;
		
		ТекущийОбъект.ВнешняяОбработка    = НовыйФайл.Ссылка;
		ЭтаФорма.ИмяФайлаВнешнейОбработки = ТекущийОбъект.ВнешняяОбработка.Наименование;
	КонецЕсли;

КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
// Открытие формы получения файла.
//
Процедура ОткрытьДиалогВыгрузки(Результат, ДополнительныеПараметры) Экспорт
	
	ОповещениеЗавершенияПолученияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайла", ЭтаФорма);
	ПолучаемыеФайлы = Новый Массив;
	Если ДополнительныеПараметры.ИмяШаблона = "" Тогда
		Если Не ЗначениеЗаполнено(АдресФайла) тогда
			ТекстСообщения = НСтр("ru = 'Файл внешней обработки еще не был загружен.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляСохранения(Объект.ВнешняяОбработка, УникальныйИдентификатор);
	РаботаСФайламиСлужебныйКлиент.СохранитьКак(Неопределено, ДанныеФайла, Неопределено);
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выгрузки файла.
//
Процедура ОбработкаЗавершенияПомещенияФайла(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПолученныеФайлы <> Неопределено Тогда
		Для каждого ПолученныйФайл Из ПолученныеФайлы Цикл
			ТекстСообщения = Нстр("ru = 'Внешняя обработка выгружена: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрЗаменить(ПолученныйФайл.Имя, "/", "\"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	Иначе
		ТекстСообщения = Нстр("ru = 'Не удалось выгрузить внешнюю обработку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 