#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет первоначальное заполнение состояний оборудывания.
//
Процедура ПервоначальноеЗаполнение() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Элемент = Справочники.упСостоянияОборудования.НеИспользуется.ПолучитьОбъект();
	Элемент.ТипСостояния = Перечисления.упТипыСостоянийОборудования.НеИспользуется;
	Элемент.Записать();
	
	Элемент = Справочники.упСостоянияОборудования.ОприходованоНаСклад.ПолучитьОбъект();
	Элемент.ТипСостояния = Перечисления.упТипыСостоянийОборудования.ОприходованоНаСклад;
	Элемент.Записать();
	
	Элемент = Справочники.упСостоянияОборудования.УстановленоНаТС.ПолучитьОбъект();
	Элемент.ТипСостояния = Перечисления.упТипыСостоянийОборудования.УстановленоНаТС;
	Элемент.Записать();
	
	Элемент = Справочники.упСостоянияОборудования.Списано.ПолучитьОбъект();
	Элемент.ТипСостояния = Перечисления.упТипыСостоянийОборудования.Списано;
	Элемент.Записать();
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // ПервоначальноеЗаполнение()

// Функция возвращает текущее Состояние типа оборудования
//
// Параметры:
//  Ссылка  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упСостоянияОборудования - Результат выполнения.
//
Функция ПолучитьСостоянияПоУмолчанию(Ссылка) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СостояниеНеИспользуетсяПоУмолчанию", Справочники.упСостоянияОборудования.НеИспользуется);
	Результат.Вставить("СостояниеОприходованоНаСкладПоУмолчанию", Справочники.упСостоянияОборудования.ОприходованоНаСклад);
	Результат.Вставить("СостояниеУстановленоНаТСПоУмолчанию", Справочники.упСостоянияОборудования.УстановленоНаТС);
	Результат.Вставить("СостояниеСписаноПоУмолчанию", Справочники.упСостоянияОборудования.Списано);
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	|	упДополнительноеОборудование.Ссылка КАК Ссылка,
	|	упТипыДополнительногоОборудования.СостояниеНеИспользуетсяПоУмолчанию КАК СостояниеНеИспользуетсяПоУмолчанию,
	|	упТипыДополнительногоОборудования.СостояниеОприходованоНаСкладПоУмолчанию КАК СостояниеОприходованоНаСкладПоУмолчанию,
	|	упТипыДополнительногоОборудования.СостояниеУстановленоНаТСПоУмолчанию КАК СостояниеУстановленоНаТСПоУмолчанию,
	|	упТипыДополнительногоОборудования.СостояниеСписаноПоУмолчанию КАК СостояниеСписаноПоУмолчанию
	|ИЗ
	|	Справочник.упДополнительноеОборудование КАК упДополнительноеОборудование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыДополнительногоОборудования КАК упТипыДополнительногоОборудования
	|		ПО упДополнительноеОборудование.ТипДополнительногоОборудования = упТипыДополнительногоОборудования.Ссылка
	|ГДЕ
	|	упТипыДополнительногоОборудования.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упШиныУзлыИАгрегаты.Ссылка,
	|	упТипыШинУзловИАгрегатов.СостояниеНеИспользуетсяПоУмолчанию,
	|	упТипыШинУзловИАгрегатов.СостояниеОприходованоНаСкладПоУмолчанию,
	|	упТипыШинУзловИАгрегатов.СостояниеУстановленоНаТСПоУмолчанию,
	|	упТипыШинУзловИАгрегатов.СостояниеСписаноПоУмолчанию
	|ИЗ
	|	Справочник.упШиныУзлыИАгрегаты КАК упШиныУзлыИАгрегаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыШинУзловИАгрегатов КАК упТипыШинУзловИАгрегатов
	|		ПО упШиныУзлыИАгрегаты.Тип = упТипыШинУзловИАгрегатов.Ссылка
	|ГДЕ
	|	упШиныУзлыИАгрегаты.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Если ЗначениеЗаполнено(Выборка.СостояниеНеИспользуетсяПоУмолчанию) Тогда
			Результат.Вставить("СостояниеНеИспользуетсяПоУмолчанию", Выборка.СостояниеНеИспользуетсяПоУмолчанию);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.СостояниеОприходованоНаСкладПоУмолчанию) Тогда
			Результат.Вставить("СостояниеОприходованоНаСкладПоУмолчанию", Выборка.СостояниеОприходованоНаСкладПоУмолчанию);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.СостояниеУстановленоНаТСПоУмолчанию) Тогда
			Результат.Вставить("СостояниеУстановленоНаТСПоУмолчанию", Выборка.СостояниеУстановленоНаТСПоУмолчанию);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.СостояниеСписаноПоУмолчанию) Тогда
			Результат.Вставить("СостояниеСписаноПоУмолчанию", Выборка.СостояниеСписаноПоУмолчанию);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСостоянияПоУмолчанию()

#КонецОбласти

#КонецЕсли