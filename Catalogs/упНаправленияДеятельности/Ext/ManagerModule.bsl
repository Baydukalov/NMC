
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упНаправленияДеятельности.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Возвращает ссылку на Направление деятельности.
//
// Параметры:
//  Ссылка  - ДокументСсылка - ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникиСсылка.упНаправленияДеятельности - Результат выполнения.
//
Функция ПодобратьНаправлениеДеятельности(Ссылка) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упНаправленияДеятельности.Ссылка
	|ИЗ
	|	Справочник.упНаправленияДеятельности КАК упНаправленияДеятельности
	|ГДЕ
	|	НЕ упНаправленияДеятельности.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА упНаправленияДеятельности.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Высокая)
	|			ТОГДА 0
	|		КОГДА упНаправленияДеятельности.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Низкая)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ";
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов[0];
	ОператорыВыбрать = ЗапросВыбораСхемыЗапроса.Операторы[0];
	КоличествоПолей = ОператорыВыбрать.ВыбираемыеПоля.Количество();
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		ОператорыВыбрать.ВыбираемыеПоля.Добавить("упНаправленияДеятельности.СхемаЗаявкаНаПеревозкуГруза");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	ЗапросВыбораСхемыЗапроса.Колонки[КоличествоПолей].Псевдоним = "СхемаКомпоновкиДанных";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
						
			СхемаКомпоновкиДанных = Выборка.СхемаКомпоновкиДанных.Получить();
			Если СхемаКомпоновкиДанных = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Ссылка", Ссылка, ВидСравненияКомпоновкиДанных.Равно,, Истина);
			
			КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
			КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
			КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			
			// Компоновка макета.
			КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
			МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
			
			// Инициализация процессора компоновки.
			ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
			ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
			
			// Таблица значений, в которую будет получен результат.
			Результат = Новый ТаблицаЗначений;
			
			// Получение результата.
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
			
			Если Результат.Количество() Тогда
				Возврат Выборка.Ссылка;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат Неопределено;

КонецФункции // ПодобратьНаправлениеДеятельности()

#КонецОбласти

#Область ОбработчикиСобытий

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
