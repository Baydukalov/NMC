
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.упТипыТС") Тогда
		сткДанныеТипаТС = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Ширина, 
																					|Длина, 
																					|Высота, 
																					|Грузоподъемность, 
																					|Объем, 
																					|КоличествоЗагрузочныхМетров, 
																					|КоличествоМест, 
																					|СнаряженнаяМасса,
																					|ТипКузова");
        	ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткДанныеТипаТС);
		ТипТС = ДанныеЗаполнения;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
		УстановитьПривилегированныйРежим(Истина);
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДоступныеМоделиТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("МодельТранспорта", Ссылка);
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Отказ = Истина;
		КонецПопытки;
		упПравилаДоступностиРесурсов.СформироватьЗаписиДоступныеМоделиТранспорта(Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти 

#КонецЕсли
