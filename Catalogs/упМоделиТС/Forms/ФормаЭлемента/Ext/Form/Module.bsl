#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Элементы.ГруппаШГВ.Видимость = УчитыватьЛинейныеРазмеры;
	УстановитьЗаголовки();
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ЗаполнитьНавигационнуюСсылкуИзображенияСервер();
	
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаКомментарий, Объект.Комментарий);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.УчитыватьМассу                       = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем                       = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест              = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьЛинейныеРазмеры             = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
		
	ЭтаФорма.КоэффициентПересчетаОбъема 		  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры") И Не ПолучитьФункциональнуюОпцию("упУчитыватьОбъем") Тогда 
		Элементы.ГруппаШГВ.Видимость              = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьОбъем") Тогда 
		Элементы.ГруппаОбъем.Видимость            = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьМассу") Тогда 
		Элементы.ГруппаГрузоподъемность.Видимость = Ложь;
		Элементы.ГруппаНагрузкаНаОсь.Видимость    = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест") Тогда 
		Элементы.ГруппаКоличествоМест.Видимость   = Ложь;
	КонецЕсли;
		
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//ПрицепПриИзменении(Неопределено);
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	РазблокироватьДанныеДляРедактирования(, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ПрисоединенныйФайл" И ЗагрузкаФайлаИзображения Тогда	
		Если ТипЗнч(Источник) = Тип("Массив") Тогда
			Если Источник.Количество() > 0 Тогда
				Объект.ИзображениеПрисоединенныйФайл = Источник[0];
				Модифицированность = Истина;
				ЗаполнитьНавигационнуюСсылкуИзображения();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипТСПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТипТС) Тогда
		
		Если Ложь
			Или Объект.Высота <> 0
			Или Объект.Длина <> 0
			Или Объект.Ширина <> 0
			Или Объект.Грузоподъемность <> 0
			Или Объект.Объем <> 0
			Или Объект.КоличествоМест <> 0
			Или Объект.КоличествоЗагрузочныхМетров <> 0
		Тогда
			ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнениеВГХПоТипуТС", ЭтаФорма);
			ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Перезаполнить ВГХ по типу транспортного средства?'"),РежимДиалогаВопрос.ДаНет);		
		Иначе
			ЗаполнитьПоТипамТС();
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры // ТипТСПриИзменении()

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Элемент.ТекущиеДанные.ДатаУстановки = ТекущаяДата();
		Элемент.ТекущиеДанные.Основной      = Ложь;
		
		Если Объект.НормыРасходаГСМ.Количество() = 1 Тогда
			Элемент.ТекущиеДанные.Основной = Истина;			
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМОсновнойПриИзменении(Элемент)
	Если Элементы.НормыРасходаГСМ.ТекущиеДанные.Основной = Истина Тогда
		Для каждого СтрокаНорма Из Объект.НормыРасходаГСМ Цикл
			СтрокаНорма.Основной = Ложь;
		КонецЦикла;
		Элементы.НормыРасходаГСМ.ТекущиеДанные.Основной  = Истина;
	Иначе
		Элементы.НормыРасходаГСМ.ТекущиеДанные.Основной  = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если НЕ ОтменаРедактирования Тогда
		
		ВидГСМ = ТекущиеДанные.ВидГСМ;
		НормаРасхода = ТекущиеДанные.НормаРасхода;
		мсвСтроки = Объект.НормыРасходаГСМ.НайтиСтроки(Новый Структура("ВидГСМ, НормаРасхода",ВидГСМ, НормаРасхода));
		
		Если мсвСтроки.Количество() > 1 Тогда
			ТекстСообщения = Нстр("ru = 'Уже есть строка с текущими значениями'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
	Иначе
		Если ТекущиеДанные.Основной Тогда
			Если Объект.НормыРасходаГСМ.Количество() > 1 Тогда
				Объект.НормыРасходаГСМ[0].Основной = Истина;
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрицепПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов();
	
	//Элементы.СтатьиРасходов.Видимость = НЕ Объект.Прицеп;
	////Элементы.УстанавливаемоеОборудование.Видимость = НЕ Объект.Прицеп;
	//Элементы.НормыРасходаГСМ.Видимость = НЕ Объект.Прицеп;
КонецПроцедуры

&НаКлиенте
Процедура ПечатнаяФормаПутевогоЛистаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("ДополнительныеОтчетыИОбработки",		"Дополнительные отчеты и обработки");
	СписокВыбора.Добавить("упТипыПечатныхФормПутевогоЛиста", 	"Типы печатных форм путевого листа");
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработкаВыбораТипаПечатныхФорм", ЭтаФорма);
	ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упМоделиТСПрисоединенныеФайлы") Тогда
		Объект.ИзображениеПрисоединенныйФайл = ВыбранноеЗначение;
		Модифицированность = Истина;
		ЗаполнитьНавигационнуюСсылкуИзображения();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеАдресНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
			ПрисоединенныеФайлыКлиент.ОткрытьФайл(ПрисоединенныеФайлыКлиент.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор));
		Иначе	
			ЗагрузитьФайлИзображения();		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаКомментарий, Объект.Комментарий);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзображение(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗагрузитьФайлИзображения();		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображение(Команда)
	Объект.ИзображениеПрисоединенныйФайл = ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
	Модифицированность = Истина;
	ЗаполнитьНавигационнуюСсылкуИзображения();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзПрисоединенныхФайлов(Команда)
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ОткрытьФормуВыбораФайлов(Объект.Ссылка,ЭтаФорма);	
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Функция УстановитьДоступностьЭлементов()

	Элементы.СтатьиРасходов.Видимость = НЕ Объект.Прицеп;
	Элементы.НормыРасходаГСМ.Видимость = НЕ Объект.Прицеп;

КонецФункции // УстановитьДоступностьЭлементов()

&НаСервере
// Выполним заполнение реквизитов объекта на сервере.
//
Процедура ЗаполнитьПоТипамТС()
	ОбъектМодельТС = РеквизитФормыВЗначение("Объект");
	ОбъектМодельТС.Заполнить(Объект.ТипТС);
	ЗначениеВРеквизитФормы(ОбъектМодельТС, "Объект");
КонецПроцедуры // ЗаполнитьПоТипамТС()

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		ЕИ_Грузов = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьЗаголовки()

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.Ширина * Объект.Высота * Объект.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
// Процедура завершения после выбора типа печатной формы из списка.
//
Процедура ОбработкаВыбораТипаПечатныхФорм(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		Если ВыбранныйЭлемент.Значение = "ДополнительныеОтчетыИОбработки" Тогда
			ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
			СписокВыбора = упСервисныеФункцииВызовСервера.ПолучитьСписокДополнительныхВнешнихПечатныхФорм(ИмяОбъектаМетаданных);
			ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработкаВыбораВнешнейПечатнойФормы", ЭтаФорма);
			ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элементы.ПечатнаяФормаПутевогоЛиста);
		ИначеЕсли ВыбранныйЭлемент.Значение = "упТипыПечатныхФормПутевогоЛиста" Тогда	
			ОткрытьФорму("Перечисление.упТипыПечатныхФормПутевогоЛиста.ФормаВыбора",,Элементы.ПечатнаяФормаПутевогоЛиста);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора внешней печатной формы из списка.
//
Процедура ОбработкаВыбораВнешнейПечатнойФормы(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		Объект.ПечатнаяФормаПутевогоЛиста = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОЗаписи(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = НЕ ПроверитьЗаполнение();
		Если НЕ Отказ Тогда
			Записать();
		КонецЕсли;
		Если НЕ Отказ Тогда
			ЗагрузитьФайлИзображения();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлИзображения()
	ЗагрузкаФайлаИзображения = Истина;
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор);
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
	ЗагрузкаФайлаИзображения = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНавигационнуюСсылкуИзображения()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНавигационнуюСсылкуИзображенияСервер()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НавигационнаяСсылкаИзображения()
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецФункции

&НаКлиенте
Процедура ОбработатьОтветНаВопросЗаполнениеВГХПоТипуТС(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоТипамТС();	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
