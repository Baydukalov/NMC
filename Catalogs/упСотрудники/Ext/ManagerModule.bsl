#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упСотрудники.Реквизиты цикл
		Если Истина
			И текРек.Имя <> "Комментарий"
			И текРек.Имя <> "ГрафикРаботы"
		тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	БлокируемыеРеквизиты.Добавить("ГрафикРаботы; ГрафикРаботыОбщий, ГрафикРаботыИндивидуальный");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//  КомандыПечати	 - ТаблицаЗначений	 - Перечислены команды.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Печать бейджика.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Справочник.упСотрудники";
	КомандаПечати.Идентификатор = "Бейджик";
	КомандаПечати.Представление = НСтр("ru = 'Бейджик'");
		
КонецПроцедуры

// Выполним проверку логина сотрудника.
//
// Параметры:
//  ЛогинСотрудника - Строка - Имя пользователя, логин.
//  Сотридник - СправочникСсылка.упСотрудники - Ссылка на сотрудника, чтобы исключить логин собственного элемента.
//
// Возвращаемое значение:
//   Булево - Истина, если не нашел совпадений, Ложь, когда найдено совпадение.
//
Функция ВыполнитьПроверкуЛогинаСотрудника(ЛогинСотрудника,Сотридник) Экспорт
	
	ТекущийЛогин = НРег(СокрЛП(ЛогинСотрудника));
	
	Если ПустаяСтрока(ТекущийЛогин)Тогда
		// нет смысла проверять пустую строку.
		Возврат Истина;
	КонецЕсли;

	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСотрудники.Ссылка КАК Сотрудник,
	|	упСотрудники.Логин КАК Логин
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудники
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Сотридник = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НЕ упСотрудники.Ссылка = &Сотридник
	|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Сотридник",Сотридник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НайденныйСотрудник = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		СотрудникЛогин = НРег(СокрЛП(Выборка.Логин));
		Если ПустаяСтрока(СотрудникЛогин) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СотрудникЛогин = ТекущийЛогин Тогда
			Структура = Новый Структура;
			Структура.Вставить("Логин",Выборка.Логин);
			Структура.Вставить("Сотрудник",Выборка.Сотрудник);			
			НайденныйСотрудник = Структура;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ НайденныйСотрудник = Неопределено Тогда
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Сотруднику ""%1"" уже установлен логин ""%2"".'", НайденныйСотрудник.Сотрудник, НайденныйСотрудник.Логин));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, НайденныйСотрудник.Сотрудник,"Логин");
		Результат = Ложь;
	КонецЕсли;
	
	НайденныйСотрудник = Неопределено;
	
	Возврат Результат;

КонецФункции // ВыполнитьПроверкуЛогинаСотрудника()

#КонецОбласти 

#Область РадотаСпечатью

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект,
//                      представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Бейджик") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Бейджик",
		НСтр("ru = 'Бейджик'"),
		ПечатьБейджика(МассивОбъектов, ОбъектыПечати),
		,
		"Справочник.упСотрудники.ПФ_MXL_Бейджик");
	КонецЕсли;	
  
КонецПроцедуры

// Формирование печатной формы Бейджика.
//
// Параметры:
//  МассивОбъектов - Массив - Объекты.
//  ОбъектыПечати - СписокЗначений  - значение - ссылка на объект;
//
// Возвращаемое значение:
//   ТабличныйДокумент - Результат выполнения.
//
Функция ПечатьБейджика(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ТТН";
	ЗаполнитьТабличныйДокументБейджика(ТабличныйДокумент, МассивОбъектов, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументБейджика(ТабличныйДокумент, МассивОбъектов, ОбъектыПечати)
	
	
	СтруктураШтрихкода = Новый Структура;
	СтруктураШтрихкода.Вставить("Ширина",                      300);
	СтруктураШтрихкода.Вставить("Высота",                      50);
	СтруктураШтрихкода.Вставить("ТипКода",                     4);
	СтруктураШтрихкода.Вставить("РазмерШрифта",                14);
	СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
	СтруктураШтрихкода.Вставить("ЦветФона",                    16777215);
	СтруктураШтрихкода.Вставить("ЦветПолос",                   0);
	СтруктураШтрихкода.Вставить("ЦветТекста",                  0);
	СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
	СтруктураШтрихкода.Вставить("ОриентацияТекста",            0);
	СтруктураШтрихкода.Вставить("ПоложениеТекста",             0);
	СтруктураШтрихкода.Вставить("УголПоворота",                0);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.упСотрудники.ПФ_MXL_Бейджик");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСотрудники.Наименование,
	               |	упСотрудники.Ссылка,
	               |	упСотрудники.Штрихкод
	               |ИЗ
	               |	Справочник.упСотрудники КАК упСотрудники
	               |ГДЕ
	               |	упСотрудники.Ссылка В(&МассивОбъектов)";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("R1C1:R7C5");
		Область.Параметры.Наименование  = текВыборка.Наименование;
		
		СтруктураШтрихкода.Вставить("ШтрихКод", текВыборка.Штрихкод);
		Область.Рисунки.ШтрихКодСотрудника.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
			
		ТабличныйДокумент.Вывести(Область);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текВыборка.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
