
#Область ПеременныеМодуляФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ОВ",?(Параметры.Свойство("ТС"),Параметры.ТС.ОсновнойВодитель, Справочники.упСотрудники.ПустаяСсылка()));	
	Список.Параметры.УстановитьЗначениеПараметра("ВОВ",?(Параметры.Свойство("ТС"),Параметры.ТС.ВторойОсновнойВодитель, Справочники.упСотрудники.ПустаяСсылка()));	
	Список.Параметры.УстановитьЗначениеПараметра("ОЭ",?(Параметры.Свойство("ТС"),Параметры.ТС.ОсновнойЭкспедитор, Справочники.упСотрудники.ПустаяСсылка()));	
	
	ПарметрыИспользованияВыбора = Новый Структура();
	
	Если Параметры.Свойство("Гараж") Тогда
		ПарметрыИспользованияВыбора.Вставить("Гараж", Параметры.Гараж);
	КонецЕсли;
	Если Параметры.Свойство("Механик") Тогда
		ПарметрыИспользованияВыбора.Вставить("Механик", Параметры.Механик);
	КонецЕсли;
	Если Параметры.Свойство("ТС") Тогда
		ПарметрыИспользованияВыбора.Вставить("ТС", Параметры.ТС);
	КонецЕсли;
	Если Параметры.Свойство("Перевозчик") Тогда
		ПарметрыИспользованияВыбора.Вставить("Перевозчик", Параметры.Перевозчик);
	КонецЕсли;
	Если Параметры.Свойство("Отбор") Тогда
		ОтборВходящий = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Параметры.Отбор);
		ПарметрыИспользованияВыбора.Вставить("Отбор", ОтборВходящий);
		Параметры.Отбор.Очистить();
	КонецЕсли;
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ПарметрыИспользованияВыбора, УникальныйИдентификатор);
	
	УстановитьПараметрыДляФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="ЗаписанСотрудник" Тогда
		Если ТипЗнч(Параметр)=ТИП("Структура") Тогда
			Если Параметр.Свойство("ЭтоНовый") Тогда
				Если Параметр.ЭтоНовый Тогда
					Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы.Очистить();
					Список.УсловноеОформление.Элементы.Очистить();
					УстановитьПараметрыДляФормы();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыДляФормы()
	
	ПарметрыИспользованияВыбора = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	Если ПарметрыИспользованияВыбора.Свойство("Отбор") Тогда
		СписокТипов = Новый Массив;
		СписокТипов.Добавить(ТИП("Массив"));
		СписокТипов.Добавить(ТИП("ФиксированныйМассив"));
		Для каждого ЭлементСтруктуры Из ПарметрыИспользованияВыбора.Отбор Цикл
			ТекущееЗначение = ЭлементСтруктуры.Значение;
			Если НЕ СписокТипов.Найти(ТипЗнч(ТекущееЗначение))=Неопределено Тогда
				ТекущееЗначение = Новый СписокЗначений();
				Для каждого ЗначениеЭлемента Из ЭлементСтруктуры.Значение Цикл
					ТекущееЗначение.Добавить(ЗначениеЭлемента,,Истина);
				КонецЦикла;
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, ЭлементСтруктуры.Ключ, ТекущееЗначение);
		КонецЦикла;
	КонецЕсли;
	Если ПарметрыИспользованияВыбора.Свойство("Перевозчик") Тогда
		СписокСотрудников = ПолучитьСотрудников(ПарметрыИспользованияВыбора.Перевозчик);
		Если СписокСотрудников.Количество() Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Ссылка", СписокСотрудников);
		Иначе
			Если ЗначениеЗаполнено(ПарметрыИспользованияВыбора.Перевозчик) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Партнер", ПарметрыИспользованияВыбора.Перевозчик);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Гараж = Неопределено;
	
	Если Истина
		И ПарметрыИспользованияВыбора.Свойство("Гараж", Гараж)
		И ПарметрыИспользованияВыбора.Свойство("Механик")
		И ЗначениеЗаполнено(Гараж)
	Тогда
		ГруппаОтбораСотрудников = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, , ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораСотрудников, "ТипСотрудника", Перечисления.упТипыСотрудников.Механик);
		ГруппаОтбораВодителейГаража = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораСотрудников, , ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораВодителейГаража, "ТипСотрудника", Перечисления.упТипыСотрудников.Водитель);
		мсвСотрудникиГаража = СотрудникиГаража(Гараж);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораВодителейГаража, "Ссылка", мсвСотрудникиГаража);
	КонецЕсли;

КонецПроцедуры // УстановитьПараметрыДляФормы()

&НаСервереБезКонтекста
// Получим список сотрудников
//
// Параметры:
//  Перевозчик  - СправочникСсылка.упПартнеры - Ссылка на перевозчика.
//
// Возвращаемое значение:
//   СписокЗначений   - Ссылки на сотрудников.
//
Функция ПолучитьСотрудников(Перевозчик)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Партнер", Перевозчик);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСотрудники.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(упСотрудники.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудники
	|ГДЕ упСотрудники.СотрудникЧислитсяВНесколькихОрганизациях ИЛИ упСотрудники.Партнер = &Партнер";
	
	Выборка = Запрос.Выполнить().Выбрать();
	СписокЗначений = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокЗначений.Добавить(Выборка.Ссылка,Выборка.Представление);
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции // ПолучитьСотрудников()

&НаСервереБезКонтекста
Функция СотрудникиГаража(Гараж)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Гараж);
	Запрос.Текст = "ВЫБРАТЬ
	|	упГаражиСотрудники.Сотрудник КАК Сотрудник
	|ИЗ
	|	Справочник.упГаражи.Сотрудники КАК упГаражиСотрудники
	|ГДЕ
	|	упГаражиСотрудники.Ссылка = &Ссылка";
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	Возврат Результат;	
	
КонецФункции

#КонецОбласти
