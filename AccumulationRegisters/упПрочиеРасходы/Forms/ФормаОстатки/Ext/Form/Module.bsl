#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьМножественныйВыбор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "МножественныйВыбор");
	Если ЕстьМножественныйВыбор <> Неопределено
		И ЕстьМножественныйВыбор 
	Тогда
		Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	Иначе
		Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Период", Параметры.Период);
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СтатьяРасходов) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СтатьяРасходов", Параметры.СтатьяРасходов, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Подразделение", Параметры.Подразделение, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АналитическийРазрез) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "АналитическийРазрез", Параметры.АналитическийРазрез, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Валюта) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Валюта", Параметры.Валюта, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ВариантРасходов) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СтатьяРасходов.ВариантРасходов", Параметры.ВариантРасходов, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;

	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ВыбратьНаКлиенте()
	
	ВыделенныеСтроки = элементы.Список.ВыделенныеСтроки;
	
	мсвРезультат = Новый Массив;
	
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		СтрокаДанные = Элементы.Список.ДанныеСтроки(ИдентификаторСтроки);
		
		сткСтрока = Новый Структура("Организация, Подразделение, СтатьяРасходов, АналитическийРазрез, НаправлениеДеятельности, Валюта, Сумма, СуммаВалюта, СуммаНДС, СуммаВалютаНДС");

		ЗаполнитьЗначенияСвойств(сткСтрока, СтрокаДанные);
		сткСтрока.Сумма = СтрокаДанные.СуммаСНДСОстаток;
		сткСтрока.СуммаНДС = СтрокаДанные.СуммаСНДСОстаток - СтрокаДанные.СуммаБезНДСОстаток;
		Если ВедетсяВалютныйУчет Тогда
			сткСтрока.СуммаВалюта = СтрокаДанные.СуммаВалютаСНДСОстаток;	
			сткСтрока.СуммаВалютаНДС = СтрокаДанные.СуммаВалютаСНДСОстаток - СтрокаДанные.СуммаВалютаБезНДСОстаток;
		Иначе
			сткСтрока.СуммаВалюта = 0;	
			сткСтрока.СуммаВалютаНДС = 0;
		КонецЕсли;

		мсвРезультат.Добавить(сткСтрока);
		
	КонецЦикла;
		
	Закрыть(мсвРезультат);
	
КонецПроцедуры
	
#КонецОбласти
