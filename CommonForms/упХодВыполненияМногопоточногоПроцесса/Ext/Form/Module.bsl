
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстЗавершения        = Параметры.ТекстЗавершения;
	ЗаголовокЗавершения    = Параметры.ЗаголовокЗавершения;
	ЗакрыватьПриЗавершении = Параметры.ЗакрыватьПриЗавершении;
	ИмяСобытияОповещения   = Параметры.ИмяСобытияОповещения;
	
	Сч = 1;
	Для Каждого текЭлем Из Параметры.ИдентификаторыЗаданий Цикл
		НоваяСтрока = ИдентификаторыЗаданий.Добавить();
		НоваяСтрока.Идентификатор = текЭлем.Значение;
		НоваяСтрока.Номер         = Сч;
		НоваяСтрока.Имя           = текЭлем.Представление;
		
		Сч = Сч + 1;
	КонецЦикла;
	
	// добавить реквизиты и элементы на форму
	ДобавляемыеРеквизиты = Новый Массив;
	
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Число"));
	ПараметрыЧисла = Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный);
	
	ТипРеквизитаИндикатора = Новый ОписаниеТипов(ТипыРеквизита, ПараметрыЧисла);
	ТипРеквизитаСостояние = Новый ОписаниеТипов("Строка");
	
	ИдЭлементаРеквизита = "Процент";
	ИдЭлементаСостояния = "Состояние";
	
	// добавить реквизиты
	Для Каждого текСтрока Из ИдентификаторыЗаданий Цикл
		РеквизитИдентификатор = ИдЭлементаРеквизита + текСтрока.Номер;
		РеквизитНаименование  = текСтрока.Имя;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(РеквизитИдентификатор, ТипРеквизитаИндикатора, , РеквизитНаименование));
		РеквизитИдентификатор = ИдЭлементаСостояния + текСтрока.Номер;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(РеквизитИдентификатор, ТипРеквизитаСостояние));
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// добавить элементы на форму и связать их с реквизитами
	Для Каждого текСтрока Из ИдентификаторыЗаданий Цикл
		ГруппаФормы = Элементы.Добавить("Группа" + текСтрока.Номер, Тип("ГруппаФормы"), ЭтаФорма);
		ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаФормы.ОтображатьЗаголовок = Ложь;
		ГруппаФормы.Отображение = ОтображениеОбычнойГруппы.Нет;
		ПолеФормыИдентификатор = ИдЭлементаРеквизита + текСтрока.Номер;
		текИндикатор = Элементы.Добавить(ПолеФормыИдентификатор, Тип("ПолеФормы"), ГруппаФормы);
		текИндикатор.Вид = ВидПоляФормы.ПолеИндикатора;
		текИндикатор.ПутьКДанным = ПолеФормыИдентификатор;
		ПолеФормыСостояние = ИдЭлементаСостояния + текСтрока.Номер;
		текСостояние = Элементы.Добавить(ПолеФормыСостояние, Тип("ПолеФормы"), ГруппаФормы);
		текСостояние.Вид = ВидПоляФормы.ПолеНадписи;
		текСостояние.ПутьКДанным = ПолеФормыСостояние;
		текСостояние.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет; 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТолькоПросмотр = Истина;
	ПодключитьОбработчикОжидания("ОбновитьПроцентВыполнения", 10);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Запускается из обрабочика ожидания.
// Инициирует обновление индикаторов. 
//
Процедура ОбновитьПроцентВыполнения()
	
	ВсеЗаданияВыполнены = Истина;
	ЕстьЗавершенныеСОшибкой = Ложь;
	ВсеЗаданияВыполнены = ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере(ЕстьЗавершенныеСОшибкой);
	Если ВсеЗаданияВыполнены Тогда 
		ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
		ТолькоПросмотр = Ложь;
		ПоказатьОповещениеПользователя(ЗаголовокЗавершения,, ТекстЗавершения, БиблиотекаКартинок.Информация32);
		Если ЗначениеЗаполнено(ИмяСобытияОповещения) Тогда 
			Оповестить(ИмяСобытияОповещения, ВладелецФормы);
		КонецЕсли;
		Если ЗакрыватьПриЗавершении И Не ЕстьЗавершенныеСОшибкой Тогда 
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Обновляет значения индикаторов
//
Функция ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере(ЕстьЗавершенныеСОшибкой = Ложь)
	
	ЗаданияВыполнены = Истина;
	
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный));
	
	Для Каждого текСтрока Из ИдентификаторыЗаданий Цикл
		Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(текСтрока.Идентификатор);
		Процент = ЭтаФорма["Процент" + текСтрока.Номер];
		Если Задание = Неопределено ИЛИ Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда 
			Процент = 100;
		Иначе
			мсвСообшений = Задание.ПолучитьСообщенияПользователю(Истина);
			Если мсвСообшений <> Неопределено Тогда
				Для Каждого текСообщение Из мсвСообшений Цикл
					Текст = текСообщение.Текст;
					Если Лев(Текст, 3) = "###" Тогда 
						КоличествоВыполнено = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "###", ""));
						Процент = Макс(Процент, КоличествоВыполнено);
					Иначе
						текСообщение.Сообщить();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Если Задание <> Неопределено Тогда
			ЭтаФорма["Состояние" + текСтрока.Номер] = Задание.Состояние;
			Если Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
				ЕстьЗавершенныеСОшибкой = Истина;
				Элементы["Состояние" + текСтрока.Номер].ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
				Элементы["Состояние" + текСтрока.Номер].Гиперссылка = Истина;
				Элементы["Состояние" + текСтрока.Номер].УстановитьДействие("Нажатие", "СостояниеНажатие");
			КонецЕсли; 
			Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
				ЗаданияВыполнены = Ложь;
			КонецЕсли;
		КонецЕсли; 
		// установить процент выполнения задания
		ЭтаФорма["Процент" + текСтрока.Номер] = Процент;
		
	КонецЦикла;
	
	Возврат ЗаданияВыполнены;
	
КонецФункции

&НаКлиенте
Процедура СостояниеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИндентификаторЗадания = ИдентификаторыЗаданий[Число(СтрЗаменить(Элемент.Имя, "Состояние", "")) - 1].Идентификатор;
	ОписаниеОшибки = ПолучитьОписаниеОшибкинаСервере(ИндентификаторЗадания);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ОписаниеОшибки, Истина, Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОписаниеОшибкиНаСервере(ИндентификаторЗадания)
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИндентификаторЗадания);
	Если Истина
		И ФоновоеЗадание <> Неопределено
		И ФоновоеЗадание.ИнформацияОбОшибке <> Неопределено 
	Тогда
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
	КонецЕсли;
	Возврат ОписаниеОшибки;

КонецФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Для каждого текСтрока Из ИдентификаторыЗаданий Цикл
		ДлительныеОперации.ОтменитьВыполнениеЗадания(текСтрока.Идентификатор);
	КонецЦикла; 
	
КонецПроцедуры // ПриЗакрытииНаСервере()


#КонецОбласти 


