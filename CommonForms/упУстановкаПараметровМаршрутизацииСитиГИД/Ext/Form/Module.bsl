
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипОткрытия = "Константы";
	ДополнительныеОграниченияСитиГИД.Очистить();
	
	Элементы.type.РежимВыбораИзСписка = Истина;
	СписокЗначений = Элементы.type.СписокВыбора;
	
	СписокЗначений.Добавить("OptimizeTime","Оптимизировать время");
	СписокЗначений.Добавить("OptimizeLength","Оптимизировать расстояние");
	
	ПолучитьИзКонстанты = Истина;
	
	Если Параметры.Свойство("ПараметрыОткрытия") Тогда
		
		ТипОткрытия = "Параметры";
		
		Если ТипЗнч(Параметры.ПараметрыОткрытия) = ТИП("Структура")Тогда
			ПолучитьИзКонстанты = Ложь;
			ЗаполнитьЗначенияСвойств(ЭтаФорма,Параметры.ПараметрыОткрытия,,"ДополнительныеОграниченияСитиГИД");
			Если Параметры.ПараметрыОткрытия.Свойство("ДополнительныеОграниченияСитиГИД")Тогда
				Если ТипЗнч(Параметры.ПараметрыОткрытия.ДополнительныеОграниченияСитиГИД) = ТИП("ТаблицаЗначений") Тогда
					ДополнительныеОграниченияСитиГИД.Загрузить(Параметры.ПараметрыОткрытия.ДополнительныеОграниченияСитиГИД);
				ИначеЕсли ТипЗнч(Параметры.ПараметрыОткрытия.ДополнительныеОграниченияСитиГИД) = ТИП("СписокЗначений") Тогда
					Для каждого ТекущееОграничение Из Параметры.ПараметрыОткрытия.ДополнительныеОграниченияСитиГИД Цикл
						НоваяСтрока = ДополнительныеОграниченияСитиГИД.Добавить();
						НоваяСтрока.Ограничение = ТекущееОграничение.Значение.Ограничение;
						НоваяСтрока.Пометка = ТекущееОграничение.Значение.Пометка;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписатьЗакрыть.Видимость		 = Ложь;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписать.Видимость				 = Ложь;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаПрименить.КнопкаПоУмолчанию		 = Истина;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписатьЗакрыть.КнопкаПоУмолчанию = Ложь;
		
	Иначе		
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаПрименить.Видимость = Ложь;
	КонецЕсли;
	
	Если ПолучитьИзКонстанты Тогда
		
		ПараметрыМаршрутизацииСитиГИД = Константы.упПараметрыМаршрутизацииСитиГИД.Получить();
		
		ТекущаяСтруктура = ПараметрыМаршрутизацииСитиГИД.Получить();
		
		Если ТипЗнч(ТекущаяСтруктура) = ТИП("Структура")Тогда
			Если ТекущаяСтруктура.Свойство("ДополнительныеОграниченияСитиГИД")Тогда
				ЗаполнитьЗначенияСвойств(ЭтаФорма,ТекущаяСтруктура,,"ДополнительныеОграниченияСитиГИД");
				ДополнительныеОграниченияСитиГИД.Загрузить(ТекущаяСтруктура.ДополнительныеОграниченияСитиГИД);
			Иначе
				ЗаполнитьЗначенияСвойств(ЭтаФорма,ТекущаяСтруктура);
			КонецЕсли;
		Иначе
			type = "OptimizeLength";
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДополнительныеОграниченияСитиГИД.Количество() = 0 Тогда
		Для каждого МетаЗначение Из Метаданные.Перечисления.упОграниченияМаршрутизаторов.ЗначенияПеречисления Цикл
			ТекущееОграничение = Перечисления.упОграниченияМаршрутизаторов[МетаЗначение.Имя];
			НоваяСтрока = ДополнительныеОграниченияСитиГИД.Добавить();
			НоваяСтрока.Ограничение = ТекущееОграничение;
			НоваяСтрока.Пометка = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
        Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Параметры прокладки маршрута были изменены, сохранить изменения, перед закрытием?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
		
	ВыполнитьЗаписьКонстанты();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ВыполнитьЗаписьКонстанты();
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Структура = Новый Структура;
	Структура.Вставить("type",             ?(ПустаяСтрока(type),"OptimizeLength",type));
	Структура.Вставить("allowYards",       allowYards);
	Структура.Вставить("allowSideWays",    allowSideWays);
	Структура.Вставить("allowGroundRoads", allowGroundRoads);
	Структура.Вставить("allowPaidRoads",   allowPaidRoads);
	Структура.Вставить("useJams",          useJams);
	Структура.Вставить("useLimitsUpd",     useLimitsUpd);
	Структура.Вставить("limitsName","");
	Структура.Вставить("useForecast",      useForecast);
	Структура.Вставить("forecastDateTimeUtc",ДАТА("00010101000000"));
	
	СписокЗначений = Новый СписокЗначений;
	Для каждого ТекущийПропуск Из ДополнительныеОграниченияСитиГИД Цикл
		СтруктураОграничения = Новый Структура();
		СтруктураОграничения.Вставить("Пометка",ТекущийПропуск.Пометка);
		СтруктураОграничения.Вставить("Ограничение",ТекущийПропуск.Ограничение);
		СписокЗначений.Добавить(СтруктураОграничения);
	КонецЦикла;
	
	Структура.Вставить("ДополнительныеОграниченияСитиГИД",СписокЗначений);
	
	Закрыть(Структура);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПриИзменении(Элемент)
	
	Если ТипОткрытия = "Константы" Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Выполним запись параметров
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьЗаписьКонстанты()
	
	Структура = Новый Структура;
	Структура.Вставить("type",             ?(ПустаяСтрока(type),"OptimizeLength",type));
	Структура.Вставить("allowYards",       allowYards);
	Структура.Вставить("allowSideWays",    allowSideWays);
	Структура.Вставить("allowGroundRoads", allowGroundRoads);
	Структура.Вставить("allowPaidRoads",   allowPaidRoads);
	Структура.Вставить("useJams",          useJams);
	Структура.Вставить("useLimitsUpd",     useLimitsUpd);
	Структура.Вставить("limitsName","");
	Структура.Вставить("useForecast",      useForecast);
	Структура.Вставить("ДополнительныеОграниченияСитиГИД",ДополнительныеОграниченияСитиГИД.Выгрузить());
	
	ХранилищеЗначения = Новый ХранилищеЗначения(Структура);

	Константы.упПараметрыМаршрутизацииСитиГИД.Установить(ХранилищеЗначения);
	
	Модифицированность = Ложь;

КонецПроцедуры // ВыполнитьЗаписьКонстанты()

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Да Тогда
        ВыполнитьЗаписьКонстанты();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 