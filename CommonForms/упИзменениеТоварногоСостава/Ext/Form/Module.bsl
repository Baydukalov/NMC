#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	 ТолькоПросмотр = Параметры.ТолькоПросмотр;
	 Элементы.ТоварныйСоставГруза.ТолькоПросмотр = ТолькоПросмотр;
 
	 ГрузовоеМесто = Параметры.ГрузовоеМесто;
	 Регистратор = Параметры.Регистратор;
	 Рейс = Параметры.Рейс;
	 ТипОперации = Параметры.ТипОперации;
	 КоличествоГрузов = Параметры.КоличествоГрузов;
	 ЗаданиеНаПеревозкуГруза = Параметры.ЗаданиеНаПеревозкуГруза;
	 ЗаданиеНаПеревозкуГрузаПредставление = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗаданиеНаПеревозкуГрузаПредставление", "");
	 
	 Если ТипЗнч(Параметры.ТоварныйСоставГрузовогоМеста) = Тип("Массив") Тогда
	 	Для каждого Строка Из Параметры.ТоварныйСоставГрузовогоМеста Цикл
	 	   НоваяСтрока = ТоварныйСоставГрузовогоМестаИсходный.Добавить();
		   ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	 КонецЕсли;
	 	 
	 ЗаполнитьТоварныйСостав();
	 
	 // 1 - Открытие для одного грузового места для редактирования или просмотра
	 // 2 - Открытие для списка грузовых мест в режиме просмотра
	 РежимРаботыФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "РежимРаботыФормы", 1);
		 	 
	 НастроитьЭлементыФормы();
	 	
 КонецПроцедуры
 
&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для каждого Строка Из ТоварныйСоставГрузовогоМеста Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			Отказ = Истина;
			Прервать;
		ИначеЕсли НЕ ЗначениеЗаполнено(Строка.Количество) Тогда
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварныйСоставГрузовогоМестаЦенаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузовогоМестаКоличествоПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузовогоМестаСтавкаНДСПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузовогоМестаСуммаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаНоменклатураПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаЕдиныйГрузПриИзменении(Элемент)
	Если Элементы.ТоварныйСоставГруза.ВыделенныеСтроки.Количество() > 1 Тогда
		ЕдиныйГруз = Элементы.ТоварныйСоставГруза.ТекущиеДанные.ЕдиныйГруз;
		Для каждого Строка Из Элементы.ТоварныйСоставГруза.ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ТоварныйСоставГруза.ДанныеСтроки(Строка);
			ДанныеСтроки.ЕдиныйГруз = ЕдиныйГруз;
		КонецЦикла;	
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПроблемаПриИзменении(Элемент)
	Если Элементы.ТоварныйСоставГруза.ВыделенныеСтроки.Количество() > 1 Тогда
		Проблема = Элементы.ТоварныйСоставГруза.ТекущиеДанные.Проблема;
		Для каждого Строка Из Элементы.ТоварныйСоставГруза.ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ТоварныйСоставГруза.ДанныеСтроки(Строка);
			ДанныеСтроки.Проблема = Проблема;
		КонецЦикла;	
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаАдресРазгрузкиПриИзменении(Элемент)
	Если Элементы.ТоварныйСоставГруза.ВыделенныеСтроки.Количество() > 1 Тогда
		АдресРазгрузки = Элементы.ТоварныйСоставГруза.ТекущиеДанные.АдресРазгрузки;
		Для каждого Строка Из Элементы.ТоварныйСоставГруза.ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ТоварныйСоставГруза.ДанныеСтроки(Строка);
			ДанныеСтроки.АдресРазгрузки = АдресРазгрузки;
		КонецЦикла;	
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаСуммаНДСПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаСуммаБезНДСПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаАналитическийРазрезПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаУпаковкаНоменклатурыПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ТекущиеДанные.НовыйТовар	= Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПередУдалением(Элемент, Отказ)
	
	Для каждого Строка Из Элемент.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элемент.ДанныеСтроки(Строка);
		Если НЕ ДанныеСтроки.НовыйТовар Тогда
			ТекстСообщения = Нстр("ru = 'Строку с номером %1 была добавлена не этим документом, её удалять нельзя. 
										|Для отмены, необходимо заполнить проблему, а в случае разгрузки еще и адрес.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка + 1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПроблемаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ТоварныйСоставГруза.ТекущиеДанные;
	Если Истина
		И ЗначениеЗаполнено(ВыбранноеЗначение)
		И ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка")
		И НЕ ЗначениеЗаполнено(ТекущиеДанные.АдресРазгрузки)
	Тогда
		Если НЕ ЗначениеЗаполнено(АдресПогрузкиПоРейсу) Тогда
			АдресПогрузкиПоРейсу = АдресПогрузки(Рейс, ГрузовоеМесто);
		КонецЕсли;	
		ТекущиеДанные.АдресРазгрузки	= АдресПогрузкиПоРейсу;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Отказ = Ложь;
	Если РежимРаботыФормы = 1 Тогда
		ПроверитьДанные(Отказ);
		Если НЕ Отказ Тогда
			Закрыть(ИзмененныйТоварныйСостав());	
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьТоварныйСостав()
	Если ТоварныйСоставГрузовогоМестаИсходный.Количество() = 0 Тогда
		тбзТоварныйСостав = упКорректировкаМаршрута.ЗаполнитьТоварныйСоставГрузовыхМест(ГрузовоеМесто);
		ТоварныйСоставГрузовогоМестаИсходный.Загрузить(тбзТоварныйСостав);
		ТоварныйСоставГрузовогоМеста.Загрузить(тбзТоварныйСостав);
          Для каждого Строка Из ТоварныйСоставГрузовогоМеста Цикл
			Строка.ЗаданиеНаПеревозкуГруза = ЗаданиеНаПеревозкуГрузаПредставление;	
		КонецЦикла;		
	Иначе
		ТоварныйСоставГрузовогоМеста.Загрузить(ТоварныйСоставГрузовогоМестаИсходный.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИзмененныйТоварныйСостав()
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ГрузовоеМесто", ГрузовоеМесто);
	сткРезультат.Вставить("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГруза);
	сткРезультат.Вставить("КоличествоГрузов", КоличествоГрузов);
	сткРезультат.Вставить("Операция", ТипОперации);
	
	мсвИзмененияГрузовогоСостава = Неопределено;
	
	Если НЕ ОбщегоНазначения.КоллекцииИдентичны(ТоварныйСоставГрузовогоМеста, ТоварныйСоставГрузовогоМестаИсходный, "Номенклатура,Проблема,АдресРазгрузки,ЕдиныйГруз,Количество,НовыйТовар,УпаковкаНоменклатуры") Тогда
		мсвИзмененияГрузовогоСостава = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТоварныйСоставГрузовогоМеста.Выгрузить(,"Номенклатура, Проблема, АдресРазгрузки, ЕдиныйГруз, Количество, УпаковкаНоменклатуры, Цена, СтавкаНДС, СуммаНДС, СуммаБезНДС, Сумма, АналитическийРазрез, НовыйТовар"));
	КонецЕсли;
	
	сткРезультат.Вставить("МассивИзмененийСостава", мсвИзмененияГрузовогоСостава);
	
	Возврат сткРезультат;
	
КонецФункции

&НаКлиенте
Функция ПроверитьДанные(Отказ)
	Для каждого Строка Из ТоварныйСоставГрузовогоМеста Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Номенклатура) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнена номенклатура'"),,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСоставГрузовогоМеста", ТоварныйСоставГрузовогоМеста.Индекс(Строка) + 1, "Номенклатура"),, Отказ);
		ИначеЕсли Строка.Количество = 0  Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено количество'"),,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСоставГрузовогоМеста", ТоварныйСоставГрузовогоМеста.Индекс(Строка) + 1, "Количество"),, Отказ);
		КонецЕсли;
		
		Если ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда
			Если ЗначениеЗаполнено(Строка.Проблема) И НЕ ЗначениеЗаполнено(Строка.АдресРазгрузки) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнен адрес разгрузки'"),,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСоставГрузовогоМеста", ТоварныйСоставГрузовогоМеста.Индекс(Строка) + 1, "АдресРазгрузки"),, Отказ);
			ИначеЕсли ЗначениеЗаполнено(Строка.АдресРазгрузки) И НЕ ЗначениеЗаполнено(Строка.Проблема) Тогда	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнена проблема'"),,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСоставГрузовогоМеста", ТоварныйСоставГрузовогоМеста.Индекс(Строка) + 1, "Проблема"),, Отказ);
			КонецЕсли;	
		КонецЕсли;
						
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;

КонецФункции

&НаСервереБезКонтекста
Функция АдресПогрузки(Рейс, ГрузовоеМесто)
	Возврат упПрохождениеТочки.ПолучитьАдресПогрузки(Рейс, ГрузовоеМесто);
КонецФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Если РежимРаботыФормы = 2 Тогда
		Элементы.ФормаЗаписатьИЗакрыть.Заголовок = Нстр("ru = 'Закрыть'");
		Элементы.ГруппаГрузовоеМесто.Видимость = Ложь;
		Элементы.ТоварныйСоставГрузаГрузовоеМесто.Видимость = Истина;
		Элементы.ТоварныйСоставГрузаЗаданиеНаПеревозкуГруза.Видимость = Истина;
		Элементы.ТоварныйСоставГруза.ТолькоПросмотр = Истина;
		Элементы.ТоварныйСоставГруза.КоманднаяПанель.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
