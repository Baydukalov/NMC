&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	упСервисныеФункции.УстановитьЗаголовкиПолейДляПоиска(ЭтаФорма);
	
	НадписьДней = НСтр("ru = 'дн.'");
	
	// Заполним список выбора для Адреса сервера электронных карт.
	СписокЗначений = Элементы.упАдресСервераЭлектронныхКарт.СписокВыбора;
	СписокЗначений.Добавить("http://{s}.tile.openstreetmap.org",			"OpenStreetMap");
	СписокЗначений.Добавить("http://{s}.tile.osm.org",						"OSM");
	СписокЗначений.Добавить("http://maps.axelot.ru/osm",					"Axelot");
	СписокЗначений.Добавить("http://{s}.tile.openstreetmap.de/tiles/osmde", "OpenStreetMap.de");
	СписокЗначений.Добавить("http://{s}.mqcdn.com/tiles/1.0.0/osm",			"MapQuest");
	СписокЗначений.Добавить("http://{s}.mqcdn.com/tiles/1.0.0/sat",			"MapQuest Aerial");
	СписокЗначений.Добавить("http://{s}.tile.thunderforest.com/transport",	"TunderForest Transport");
	СписокЗначений.Добавить("http://{s}.tile.thunderforest.com/cycle",		"TunderForest Cycle");
	СписокЗначений.Добавить("http://{s}.tile.thunderforest.com/outdoors",	"TunderForest Outdoors");	
	                                          
	АдресаСерверовЭлектронныхКарт.ЗагрузитьЗначения(СписокЗначений.ВыгрузитьЗначения());
		
	// Заполним список выбора для пути к библиотекам Leaflet.
	СписокЗначений = Элементы.упАдресСервераГеокодированияNominatim.СписокВыбора;
	СписокЗначений.Добавить("https://nominatim.openstreetmap.org/");
	
	Элементы.упАдресСервераГеокодированияNominatim.СписокВыбора.ЗагрузитьЗначения(СписокЗначений.ВыгрузитьЗначения());	
	
	СписокЗначений = Элементы.упАдресСервераГеокодированияСитиГИД.СписокВыбора;
	СписокЗначений.Добавить("api.probki.net");
	
	Элементы.упАдресСервераГеокодированияСитиГИД.СписокВыбора.ЗагрузитьЗначения(СписокЗначений.ВыгрузитьЗначения());
	ПеречитатьДополнительныеОграниченияСитиГИД();
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Массив = Новый Массив;
		Для каждого СтруктураЗначения Из Перечисления.упТипыМаршрутизаторов.ПолучитьДанныеВыбора(Новый Структура("Отбор")).ВыгрузитьЗначения() Цикл 
			Если СтруктураЗначения.Значение <> Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
				Массив.Добавить(СтруктураЗначения.Значение);
			КонецЕсли; 
		КонецЦикла;
		упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.упТипМаршрутизатора, "Ссылка", Массив);
		Массив = Новый Массив;
		Для каждого СтруктураЗначения Из Перечисления.упТипыГеокодеров.ПолучитьДанныеВыбора(Новый Структура("Отбор")).ВыгрузитьЗначения() Цикл 
			Если СтруктураЗначения.Значение <> Перечисления.упТипыГеокодеров.Google Тогда
				Массив.Добавить(СтруктураЗначения.Значение);
			КонецЕсли; 
		КонецЦикла;
		упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.упТипГеокодера, "Ссылка", Массив);
		Массив = Новый Массив;
		Для каждого СтруктураЗначения Из Перечисления.упТипыКарт.ПолучитьДанныеВыбора(Новый Структура("Отбор")).ВыгрузитьЗначения() Цикл 
			Если СтруктураЗначения.Значение <> Перечисления.упТипыКарт.GoogleMaps Тогда
				Массив.Добавить(СтруктураЗначения.Значение);
			КонецЕсли; 
		КонецЦикла;
		упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.упТипКарт, "Ссылка", Массив);
	КонецЕсли; 
	Элементы.упПакетныйРежимОтправкиЗапросаСервису.Доступность = Не ОбщегоНазначенияПовтИсп.РазделениеВключено();
	//Элементы.упРассчитыватьВремяПоДнямНеделиСитиГИД.Доступность = Не ОбщегоНазначенияПовтИсп.РазделениеВключено();
	//Элементы.упУчитыватьКоэффициентыРасчетаВремениВПути.Доступность = Не ОбщегоНазначенияПовтИсп.РазделениеВключено();
	Элементы.ГруппаСборСтатистики.Видимость = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	Если НЕ ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Элементы.ДекорацияОтступДляРазрешитьВыпискуПутевогоЛистаДляНесколькихРейсов.Видимость = Ложь;
		Элементы.ГруппаРазрешитьВыпискуПутевогоЛистаДляНесколькихРейсов.Видимость = Ложь;
	КонецЕсли;
	
	сткВидыПеревозок = Справочники.упВидыПеревозок.ЕстьУслугиСпецтехникиИлиПассажирПеревозки();
	Элементы.упИспользуютсяЗаявкиНаПассажирскиеПеревозки.Доступность = НЕ сткВидыПеревозок.ЕстьПассажирскиеПеревозки;
	Элементы.упИспользуютсяЗаявкиНаСпецтехнику.Доступность           = НЕ сткВидыПеревозок.ЕстьУслугиСпецтехники;
	Если НЕ ПолучитьФункциональнуюОпцию("упИспользуютсяЗаявкиНаПассажирскиеПеревозки") И сткВидыПеревозок.ЕстьПассажирскиеПеревозки Тогда
		Элементы.упИспользуютсяЗаявкиНаПассажирскиеПеревозки.Доступность=Истина;
	КонецЕсли;
	Если НЕ ПолучитьФункциональнуюОпцию("упИспользуютсяЗаявкиНаСпецтехнику") И сткВидыПеревозок.ЕстьУслугиСпецтехники Тогда
		Элементы.упИспользуютсяЗаявкиНаСпецтехнику.Доступность=Истина;
	КонецЕсли;
	// Чтобы картинки не обрезались по высоте и не мешались в макете, мы им ставим высоту 1 в макете, 0 - при создании
	Элементы.КартинкаУТ.Высота = 0;
	Элементы.КартинкаУП.Высота = 0;
	Элементы.КартинкаСМ.Высота = 0;
	ОтобразитьСостояниеИспользованияКомпонент();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	УстановитьЗаголовокКоэффициентаПересчетаМассы();
	ЭтаФорма.ОповеститьРМПланированиеРейсов = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	Если ОповеститьРМПланированиеРейсов Тогда
		Оповестить("ИзменениеРежимаИспользованияЭлектронныхГеографическихКарт");
	КонецЕсли;
	ОбновитьИнтерфейс();
	Оповестить("ИзмененыПараметрыУчетаТС");
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	упСервисныеФункцииКлиент.СтрокаПоискаЭлементовФормыПриИзменении(ЭтаФорма, Элементы.РазделыСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтрокаПоискаПриИзменении(Элементы.СтрокаПоиска);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	// Взаиморасчеты
	Если НЕ НаборКонстант.упЗагружатьВзаиморасчетыСПартнерами Тогда
		НаборКонстант.упВзаиморасчетыИспользоватьАутентификациюОС = Ложь;
	КонецЕсли;
	Элементы.упВзаиморасчетыПутьКСервису.Доступность = НаборКонстант.упЗагружатьВзаиморасчетыСПартнерами;
	Элементы.упВзаиморасчетыПароль.Доступность       = НаборКонстант.упЗагружатьВзаиморасчетыСПартнерами И НЕ НаборКонстант.упВзаиморасчетыИспользоватьАутентификациюОС;
	Элементы.упВзаиморасчетыПользователь.Доступность = НаборКонстант.упЗагружатьВзаиморасчетыСПартнерами И НЕ НаборКонстант.упВзаиморасчетыИспользоватьАутентификациюОС;
	
	// ГрузыИНормативноСправочнаяИнформация
	Элементы.упЕдиницаИзмеренияГрузовыхМест.Доступность      = НаборКонстант.упУчитыватьКоличествоМест;
	Элементы.упЕдиницаИзмеренияЛинейныхГабаритов.Доступность = НаборКонстант.упУчитыватьЛинейныеРазмеры;
	Элементы.упЕдиницаИзмеренияМассы.Доступность             = НаборКонстант.упУчитыватьМассу;
	Элементы.упЕдиницаИзмеренияОбъема.Доступность            = НаборКонстант.упУчитыватьОбъем;
	Элементы.упКоэффициентПересчетаОбъема.Доступность        = НаборКонстант.упУчитыватьОбъем И НаборКонстант.упУчитыватьЛинейныеРазмеры;

	// КартыИРасчетРасстояний
	УстановитьДоступностьОбщая();
	УстановитьДоступностьКарт();
	УстановитьВидимостьАдресаСервераМаршрутизации();
	УстановитьДоступностьГеокодера();
	Элементы.упМаксимальноеКоличествоТочекМаршрутизатора.Видимость = НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.GoogleMaps");
	Элементы.ГруппаКритерийБлизостиТочек.Доступность = НаборКонстант.упПрямойРасчетГеографическиБлизкихТочекПриНеудаче;
	Элементы.упПорядокПрямогоРасчетаГеографическиБлизкихТочек.Доступность = Ложь
															  Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.AxelotMaps")
															  Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.Graphhopper");
															  
	Элементы.упAPIКлючСервисаDaData.Доступность = НаборКонстант.упНормализоватьАдресаСервисомDaData;
	Элементы.упСекретныйКлючСервисаDaData.Доступность = НаборКонстант.упНормализоватьАдресаСервисомDaData;
														  
		
	// ОбщиеНастройкиУправленияПеревозками
	Элементы.упВалютаУправленческогоУчета.Доступность 	= НаборКонстант.упВедетсяВалютныйУчет;
	Элементы.упВестиУчетРасчетовСЗаказчиками.Доступность 	= НаборКонстант.упУказываютсяФактическиОказанныеУслуги;
	Элементы.упИспользуетсяУчетРасходовНаТС.Доступность 	= НаборКонстант.упУказываютсяФактическиеРасходыПоРейсу;
	Элементы.упВерсияМобильногоКлиента.Доступность = НаборКонстант.упИспользуетсяАндроидКлиент;
	Элементы.упВерсияМобильногоКлиента.АвтоОтметкаНезаполненного = НаборКонстант.упИспользуетсяАндроидКлиент;
	Элементы.упКлючСервисаМаршрутизацииGoogleМобильныйКлиент.Доступность = НаборКонстант.упИспользуетсяАндроидКлиент;
	
	Если НЕ Элементы.упВестиУчетРасчетовСЗаказчиками.Доступность Тогда
		НаборКонстант.упВестиУчетРасчетовСЗаказчиками = Ложь;
	КонецЕсли;
	
	Если НЕ Элементы.упИспользуетсяУчетРасходовНаТС.Доступность Тогда
		НаборКонстант.упИспользуетсяУчетРасходовНаТС = Ложь;
	КонецЕсли;
	
	Элементы.упИспользуетсяЗагрузкаДанныхИзФайловТахографа.Доступность = НаборКонстант.упУчитыватьРежимТрудаИОтдыхаСотрудников;
	Если НЕ Элементы.упИспользуетсяЗагрузкаДанныхИзФайловТахографа.Доступность Тогда
		НаборКонстант.упИспользуетсяЗагрузкаДанныхИзФайловТахографа = Ложь;	
	КонецЕсли;
	Элементы.упВедетсяРозничнаяПродажаВРейсе.Доступность = НаборКонстант.упИспользуютсяУслугиИнкассации И НаборКонстант.упИспользуетсяАндроидКлиент;
	
	Если Не НаборКонстант.упИспользуютсяУслугиИнкассации Или Не НаборКонстант.упИспользуетсяАндроидКлиент И НаборКонстант.упВедетсяРозничнаяПродажаВРейсе Тогда
		НаборКонстант.упВедетсяРозничнаяПродажаВРейсе = Ложь;
	КонецЕсли;
	
	// ТранспортныеСредстваИПланирование
	Элементы.упСтатистикаНахождениеВТочкахПартнеровПериодАнализа.Доступность                = НаборКонстант.упСтатистикаНахождениеВТочкахПартнеровУчитывать;
	Элементы.упСтатистикаНахождениеВТочкахПартнеровТребуемоеКоличествоПосещений.Доступность = НаборКонстант.упСтатистикаНахождениеВТочкахПартнеровУчитывать;  
	Элементы.ГруппаНастройиАвторизацииСервиса.Доступность = НаборКонстант.упИспользуетсяИнтеграцияСPacker3D;  

	// УправлениеТранспортом
	Элементы.упИспользуетсяУчетПутевыхЛистов.Доступность                        = НаборКонстант.упИспользуетсяУчетГСМ;
	Элементы.упВестиНумерациюПутевыхЛистовВРазрезеОрганизаций.Доступность       = НаборКонстант.упИспользуетсяУчетПутевыхЛистов;
	Элементы.упМетодОценкиСтоимостиМатериалов.Доступность                       = НаборКонстант.упИспользуетсяУчетМатериалов;
	Элементы.упРазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов.Доступность = НаборКонстант.упИспользуетсяУчетПутевыхЛистов;
	Элементы.упИспользуетсяЗагрузкаДанныхОтТопливныхКомпаний.Доступность        = НаборКонстант.упИспользуетсяУчетГСМ;
	Элементы.упИспользуютсяЛимитыРасходаГСМ.Доступность                         = НаборКонстант.упИспользуетсяУчетГСМ;
	Элементы.упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса.Доступность = Ложь
																	Или НаборКонстант.упУчитыватьЛинейныеРазмеры
																	Или НаборКонстант.упУчитыватьМассу
																	Или НаборКонстант.упУчитыватьОбъем
																	Или НаборКонстант.упУчитыватьКоличествоМест
																	Или НаборКонстант.упУчитыватьКоличествоЗагрузочныхМетров;
	Если Истина 
		И НЕ Элементы.упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса.Доступность
		И НаборКонстант.упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса
	Тогда
		НаборКонстант.упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса = Ложь;	
	КонецЕсли;																
	
КонецПроцедуры

&НаКлиенте
Процедура упЗагружатьВзаиморасчетыСПартнерамиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упВзаиморасчетыИспользоватьАутентификациюОСПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упЕдиницаИзмеренияМассыПриИзменении(Элемент)
	
	УстановитьЗаголовокКоэффициентаПересчетаМассы();
	
КонецПроцедуры
	
&НаКлиенте
Процедура упУчитыватьКоличествоМестПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упУчитыватьМассуПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упУчитыватьОбъемПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упУчитыватьЛинейныеРазмерыПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упУчитыватьКоличествоЗагрузочныхМетровПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокКоэффициентаПересчетаМассы()
	ТекстЗаголовка = Нстр("ru = 'Коэффициент пересчета массы из кг в %1:'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, НаборКонстант.упЕдиницаИзмеренияМассы);
	Элементы.ДекорацияЗаголовокКоэффициентаПересчетаМассы.Заголовок = ТекстЗаголовка;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если НЕ ЗначениеЗаполнено(НаборКонстант.упТипКарт)  Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите тип используемой электронной карты!'"),,"упТипКарт","НаборКонстант", Отказ);
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если ЗначениеЗаполнено(НаборКонстант.упТипКарт)  Тогда
			Если ПустаяСтрока(НаборКонстант.упКлючЭлектронныхГеографическихКарт) 
				И (НЕ НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.OSM") И НЕ НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps")) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите ключ доступа используемой электронной географической карты!'"),,"упКлючЭлектронныхГеографическихКарт","НаборКонстант", Отказ);
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	Если НаборКонстант.упИспользуетсяПостроительМаршрутов Тогда
		Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.GoogleMaps") Тогда
			Если ПустаяСтрока(НаборКонстант.упКлючСервисаМаршрутизации)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите API ключ сервиса маршрутизации GoogleMaps!'"),,"упКлючСервисаМаршрутизации","НаборКонстант", Отказ);
			КонецЕсли;
		ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.Graphhopper") Тогда
			// ключ не указываем
		ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.AxelotMaps") Тогда
			// ключ не указываем
		ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.СитиГИД") Тогда			
			Если ПустаяСтрока(НаборКонстант.упКлючДоступаСервисаМаршрутизацииСитиГИД)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите API ключ сервиса маршрутизации СитиГИД!'"),,"упКлючДоступаСервисаМаршрутизацииСитиГИД","НаборКонстант", Отказ);
			КонецЕсли;
			Если ПустаяСтрока(НаборКонстант.упРесурсСитиГИД)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите Ресурс СитиГИД сервиса маршрутизации СитиГИД!'"),,"упРесурсСитиГИД","НаборКонстант", Отказ);
			КонецЕсли;
			Если ПустаяСтрока(НаборКонстант.упАдресСервераМаршрутизацииСитиГИД)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите Адрес сервера сервиса маршрутизации СитиГИД!'"),,"упАдресСервераМаршрутизацииСитиГИД","НаборКонстант", Отказ);
			КонецЕсли;
			Если НЕ Отказ Тогда
				ВыполнитьЗаписьОграниченийСитиГИД();
			КонецЕсли;
		ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексКарты") Тогда
			Если ПустаяСтрока(НаборКонстант.упКлючСервисаМаршрутизации)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите API ключ сервиса маршрутизации Яндекс!'"),,"упКлючСервисаМаршрутизации","НаборКонстант", Отказ);
			КонецЕсли;
		ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексМаршрутизация") Тогда
			Если ПустаяСтрока(НаборКонстант.упКлючСервисаМаршрутизации)Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите API ключ сервиса Яндекс.Маршрутизация!'"),,"упКлючСервисаМаршрутизации","НаборКонстант", Отказ);
			КонецЕсли;
		КонецЕсли;
		Если Истина
			И НаборКонстант.упПрямойРасчетГеографическиБлизкихТочекПриНеудаче
			И НаборКонстант.упКритерийБлизостиТочекВМетрах = 0 
		Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните расстояние для расчета близко лежащих точек!'"),,"упКритерийБлизостиТочекВМетрах","НаборКонстант", Отказ);
		КонецЕсли;
	КонецЕсли;
	Если НаборКонстант.упИспользуетсяГеокодированиеАдресов Тогда
		Если Не ЗначениеЗаполнено(НаборКонстант.упТипГеокодера) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите тип геокодера!'"),, "упТипГеокодера", "НаборКонстант", Отказ);
		ИначеЕсли Истина
			И ПустаяСтрока(НаборКонстант.упКлючСервисаГеоКодирования) 
			И НЕ (Ложь
				Или НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Nominatim") 
				Или НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.AxelotMaps"))
		Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите API ключ сервиса геокодирования!'"),, "упКлючСервисаГеоКодирования", "НаборКонстант", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если НаборКонстант.упНормализоватьАдресаСервисомDaData Тогда
		
		ОбъектМетаданных = Неопределено;
		
		Если Не ЗначениеЗаполнено(НаборКонстант.упAPIКлючСервисаDaData) Тогда
			ОбъектМетаданных = Метаданные.Константы.упAPIКлючСервисаDaData;
		ИначеЕсли Не ЗначениеЗаполнено(НаборКонстант.упСекретныйКлючСервисаDaData) Тогда
			ОбъектМетаданных = Метаданные.Константы.упСекретныйКлючСервисаDaData;
		КонецЕсли;
		
		Если ОбъектМетаданных <> Неопределено Тогда
			ТекстСообщения = стрШаблон(НСтр("ru = 'Укажите %1 сервиса DaData!'"),ОбъектМетаданных.Представление());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ОбъектМетаданных.Имя, "НаборКонстант", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура упТипМаршрутизатораПриИзменении(Элемент)
	
	Если НЕ НаборКонстант.упТипМаршрутизатора = ТекущееЗначениеЭлемента Тогда
		Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.AxelotMaps") Тогда			
			ЛегальностьИспользованияСервисаAxelotMaps(Элемент.Имя);		
		Иначе
			ЛегальностьИспользованияСервиса(Элемент.Имя);
		КонецЕсли;
		Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.СитиГИД") Тогда			
			НаборКонстант.упПакетныйРежимЗапросовСервисуМаршрутизации = Истина;
		КонецЕсли; 
		
		Если Ложь
			Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.СитиГИД")
			Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.GoogleMaps")
			Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексКарты")
			Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексМаршрутизация")
		Тогда
			НаборКонстант.упПорядокПрямогоРасчетаГеографическиБлизкихТочек = ПредопределенноеЗначение("Перечисление.упПорядокПрямогоРасчетаГеографическиБлизкихТочек.ПриНеудачеМаршрутизатора");	
		КонецЕсли;
		
	КонецЕсли;	
	ПриИзмененииТипМаршрутизатора();	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТипМаршрутизатора()
	ТекущееЗначениеЭлемента = Неопределено;
	УстановитьДоступность();
КонецПроцедуры // ПриИзмененииТипМаршрутизатора()

&НаКлиенте
Процедура упТипМаршрутизатораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущееЗначениеЭлемента = НаборКонстант.упТипМаршрутизатора;
КонецПроцедуры

&НаКлиенте
Процедура упИспользуютсяЭлектронныеГеографическиеКартыПриИзменении(Элемент)

	УстановитьДоступностьОбщая();
	УстановитьДоступностьКарт();
	ОповеститьРМПланированиеРейсов = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяГеокодированиеАдресовПриИзменении(Элемент)
	
	УстановитьДоступностьОбщая();
	УстановитьДоступностьГеокодера();
	
КонецПроцедуры

&НаКлиенте
Процедура упТипГеокодераПриИзменении(Элемент)
	
	Если НЕ НаборКонстант.упТипГеокодера = ТекущееЗначениеЭлемента Тогда
		Если НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.AxelotMaps") Тогда			
			ЛегальностьИспользованияСервисаAxelotMaps(Элемент.Имя);
		ИначеЕсли НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Яндекс") Тогда
			ЛегальностьИспользованияСервисаЯндекс(Элемент.Имя);
		Иначе
			ЛегальностьИспользованияСервиса(Элемент.Имя);
		КонецЕсли;	
	КонецЕсли;
	ТекущееЗначениеЭлемента = Неопределено;
		
КонецПроцедуры

&НаКлиенте
Процедура упТипГеокодераНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущееЗначениеЭлемента = НаборКонстант.упТипГеокодера;
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяПостроительМаршрутовПриИзменении(Элемент)
	
	УстановитьДоступностьОбщая();
	УстановитьВидимостьАдресаСервераМаршрутизации();
	
КонецПроцедуры

&НаКлиенте
Процедура упТипКартПриИзменении(Элемент)
	
		Если НЕ НаборКонстант.упТипКарт = ТекущееЗначениеЭлемента Тогда
		
		Если НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps") Тогда			
			ЛегальностьИспользованияСервисаAxelotMaps(Элемент.Имя);			
		Иначе
			ЛегальностьИспользованияСервиса(Элемент.Имя);
		КонецЕсли;
		
	КонецЕсли;	
	
	ТекущееЗначениеЭлемента = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура упТипКартНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущееЗначениеЭлемента = НаборКонстант.упТипКарт;
	
КонецПроцедуры

&НаКлиенте
Процедура упРассчитыватьРасстоянияТолькоМеждуГеографическиБлизкимиТочкамиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, вхПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НаборКонстант.упКлючЭлектронныхГеографическихКарт = "";
	КонецЕсли;
	
КонецПроцедуры //ПослеЗакрытияВопроса()

&НаКлиенте
Процедура НадписьСсылкаНажатие(Элемент)
	
	Если НЕ ПустаяСтрока(Элементы.НадписьСсылка.Заголовок) Тогда
		ЗапуститьПриложение(Элементы.НадписьСсылка.Заголовок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИДПриИзменении(Элемент)
	
	Если НаборКонстант.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД Тогда
		Элементы.ДополнительныеОграниченияСитиГИД.ТолькоПросмотр = Ложь;
	иначе
		Элементы.ДополнительныеОграниченияСитиГИД.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеОграниченияСитиГИДПометкаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеОграниченияСитиГИДПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура кмдПроверитьКарту(Команда)
	
	#Если ВебКлиент Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Проверка работы карты в веб-клиенте не поддерживается.'");
		Сообщение.Сообщить();
	#Иначе	
		
		Если ПустаяСтрока(НаборКонстант.упКлючЭлектронныхГеографическихКарт) 
			И (НЕ НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.OSM")
			И НЕ НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps")) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите ключ доступа используемой электронной географической карты!'"),,"упКлючЭлектронныхГеографическихКарт","НаборКонстант");
			Возврат;
		КонецЕсли;
		
		Если НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.ЯндексКарты")Тогда		
			ТекстКлюча = упМаршрутизация.ПолучитьТекстКлючаAPI(НаборКонстант.упКлючЭлектронныхГеографическихКарт, НаборКонстант.упТипКарт);		
			ПроверяемыйКлюч = СтрЗаменить(НРег(ТекстКлюча),НРег("&apikey="),"");
			Если НЕ ПустаяСтрока(ПроверяемыйКлюч) Тогда
				HttpКоманда = "https://enterprise.api-maps.yandex.ru/2.1/?lang=ru_RU&load=package.map" + ТекстКлюча;		
				РезультатПолученияФайлов = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(HttpКоманда);
				Если НЕ РезультатПолученияФайлов.Статус Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "При выполнении проверки ключа найдена ошибка ...";
					Сообщение.Сообщить();				
					
					ЧтениеJSON = Новый ЧтениеJSON;
					ЧтениеJSON.УстановитьСтроку(РезультатПолученияФайлов.СообщениеОбОшибке);
					Данные = ПрочитатьJSON(ЧтениеJSON, Ложь);
					ЧтениеJSON.Закрыть();
					Если Данные.Свойство("error") Тогда
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = "Текст ошибки: " + Данные.error;
						Сообщение.Сообщить();
					КонецЕсли;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыПроверки = Новый Структура();
		ПараметрыПроверки.Вставить("упТипКарт"                              ,НаборКонстант.упТипКарт);
		ПараметрыПроверки.Вставить("упАдресСервераЭлектронныхКарт"          ,НаборКонстант.упАдресСервераЭлектронныхКарт);
		ПараметрыПроверки.Вставить("упКлючЭлектронныхГеографическихКарт"    ,НаборКонстант.упКлючЭлектронныхГеографическихКарт);
				
		ОткрытьФорму("ОбщаяФорма.упКартыПроверкаРаботы", ПараметрыПроверки,ЭтаФорма,, ВариантОткрытияОкна.ОтдельноеОкно,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура кмдПроверка(Команда)	
	
	ПараметрыПроверки = Новый Структура();
	ПараметрыПроверки.Вставить("упТипМаршрутизатора",НаборКонстант.упТипМаршрутизатора);
	ПараметрыПроверки.Вставить("упКлючСервисаМаршрутизации",НаборКонстант.упКлючСервисаМаршрутизации);
	ПараметрыПроверки.Вставить("упКлючДоступаСервисаМаршрутизацииСитиГИД",НаборКонстант.упКлючДоступаСервисаМаршрутизацииСитиГИД);
	ПараметрыПроверки.Вставить("упАдресСервераМаршрутизацииСитиГИД",НаборКонстант.упАдресСервераМаршрутизацииСитиГИД);
	ПараметрыПроверки.Вставить("упАдресСервераМаршрутизацииЯндекс",НаборКонстант.упАдресСервераМаршрутизацииЯндекс);
	ПараметрыПроверки.Вставить("упАдресСервераМаршрутизацииGraphhopper",НаборКонстант.упАдресСервераМаршрутизацииGraphhopper);
	ПараметрыПроверки.Вставить("упДополнительныеПараметрыЗапросаМаршрутизатора",НаборКонстант.упДополнительныеПараметрыЗапросаМаршрутизатора);
	ПараметрыПроверки.Вставить("упРесурсСитиГИД",НаборКонстант.упРесурсСитиГИД);
	ПараметрыПроверки.Вставить("упПарольСервисаМаршрутизацииИКартографииAXELOT",НаборКонстант.упПарольСервисаМаршрутизацииИКартографииAXELOT);
	ПараметрыПроверки.Вставить("упПользовательСервисаМаршрутизацииИКартографииAXELOT",НаборКонстант.упПользовательСервисаМаршрутизацииИКартографииAXELOT);	
	ПараметрыПроверки.Вставить("ВремяОтправленияПоУмолчаниюЯндексМаршрутизация",НаборКонстант.упВремяОтправленияПоУмолчаниюЯндексМаршрутизация);	
	
	//ПараметрыПроверки.Вставить("упИспользоватьТестовоеПодключениеМаршрутизация",НаборКонстант.упИспользоватьТестовоеПодключениеМаршрутизация);
	
	РезультатПроверки = ВыполнитьПроверкуСервисаМаршрутизации(ПараметрыПроверки);
	
	Если РезультатПроверки.Статус Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Соединение установлено'"));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Соединение не установлено'"));
		ТекстОшибки = НСтр("ru = 'Не удалось установить соединение по причине: ""%1""'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, РезультатПроверки.Ошибка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьКлючМаршрутизации(Команда)
	
	ПараметрыОтправки = Новый Структура();
	ПараметрыОтправки.Вставить("упТипМаршрутизатора",НаборКонстант.упТипМаршрутизатора);
	ПараметрыОтправки.Вставить("упКлючСервисаМаршрутизации",НаборКонстант.упКлючСервисаМаршрутизации);
	ПараметрыОтправки.Вставить("упАдресСервераМаршрутизацииЯндекс",НаборКонстант.упАдресСервераМаршрутизацииЯндекс);
	
	РезультатПроверки = ВыполнитьОтправкуКлючаСервисаМаршрутизации(ПараметрыОтправки);
	
	Если РезультатПроверки.Статус Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'API ключ установлен для сервиса маршрутизации'"));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Соединение не установлено. Ошибка установки API ключа'"));
		ТекстОшибки = НСтр("ru = 'Не удалось установить соединение по причине: ""%1""'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, РезультатПроверки.Ошибка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура кмдПроверитьГеокодер(Команда)
	
	ПараметрыПроверки = Новый Структура();
	ПараметрыПроверки.Вставить("упТипГеокодера",НаборКонстант.упТипГеокодера);
	ПараметрыПроверки.Вставить("упАдресСервераГеокодирования",НаборКонстант.упАдресСервераГеокодирования);
	ПараметрыПроверки.Вставить("упАдресСервераГеокодированияNominatim",НаборКонстант.упАдресСервераГеокодированияNominatim);
	ПараметрыПроверки.Вставить("упАдресСервераГеокодированияСитиГИД",НаборКонстант.упАдресСервераГеокодированияСитиГИД);	
	ПараметрыПроверки.Вставить("упКлючСервисаГеоКодирования",НаборКонстант.упКлючСервисаГеоКодирования);
	ПараметрыПроверки.Вставить("упПределОтветовГеосервиса",НаборКонстант.упПределОтветовГеосервиса);
	ПараметрыПроверки.Вставить("упАдресСервераГеокодированияСитиГИД",НаборКонстант.упАдресСервераГеокодированияСитиГИД);
	ПараметрыПроверки.Вставить("упНормализоватьАдресаСервисомDaData", НаборКонстант.упНормализоватьАдресаСервисомDaData);
	ПараметрыПроверки.Вставить("упСекретныйКлючСервисаDaData", НаборКонстант.упСекретныйКлючСервисаDaData);
	ПараметрыПроверки.Вставить("упAPIКлючСервисаDaData", НаборКонстант.упAPIКлючСервисаDaData);
	
	РезультатПроверки = упГеокодированиеКлиентСервер.ВыполнитьПровекруСервисаГеокодирования(ПараметрыПроверки);
	
	Если РезультатПроверки.Статус Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Соединение установлено'"));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Соединение не установлено'"));
		ТекстОшибки = НСтр("ru = 'Не удалось установить соединение по причине: ""%1""'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, РезультатПроверки.Ошибка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура кмдПараметрыСитиГИД(Команда)
	
	ИзмененыОграничения = ВыполнимПроверкуИзмененияОграничений();
	Если ИзмененыОграничения Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаИзмененыОграничения", ЭтаФорма, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Были изменены дополнительные ограничения СитиГИД, сохранить изменения?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
	Иначе
		ОткрытьФорму("ОбщаяФорма.упУстановкаПараметровМаршрутизацииСитиГИД",,ЭтаФорма,УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаИзмененыОграничения(Результат, Параметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
		ПеречитатьДополнительныеОграниченияСитиГИД();
	Иначе
		ВыполнитьЗаписьОграниченийСитиГИД();
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.упУстановкаПараметровМаршрутизацииСитиГИД",,ЭтаФорма,УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьДополнительныеОграниченияСитиГИД()
	
	ДополнительныеОграниченияСитиГИД.Очистить();
	ПараметрыМаршрутизацииСитиГИД = Константы.упПараметрыМаршрутизацииСитиГИД.Получить();
	ТекущаяСтруктура = ПараметрыМаршрутизацииСитиГИД.Получить();	
	ТаблицаОграничений = Неопределено;
	Если ТипЗнч(ТекущаяСтруктура) = ТИП("Структура")Тогда
		Если ТекущаяСтруктура.Свойство("ДополнительныеОграниченияСитиГИД") Тогда
			ТаблицаОграничений = ТекущаяСтруктура.ДополнительныеОграниченияСитиГИД;	
		КонецЕсли;
	КонецЕсли;
	Для каждого МетаЗначение Из Метаданные.Перечисления.упОграниченияМаршрутизаторов.ЗначенияПеречисления Цикл
		ТекущееОграничение = Перечисления.упОграниченияМаршрутизаторов[МетаЗначение.Имя];
		Если ДополнительныеОграниченияСитиГИД.НайтиСтроки(Новый Структура("Ограничение", ТекущееОграничение)).Количество() = 0 Тогда
			НоваяСтрока = ДополнительныеОграниченияСитиГИД.Добавить();
			НоваяСтрока.Ограничение = ТекущееОграничение;
			НоваяСтрока.Пометка = Ложь;
			Если ТаблицаОграничений <> Неопределено Тогда
				СтрокаСохраненногоЗначения = ТаблицаОграничений.Найти(ТекущееОграничение, "Ограничение");
				Если СтрокаСохраненногоЗначения <> Неопределено Тогда
					НоваяСтрока.Пометка = СтрокаСохраненногоЗначения.Пометка;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // ПеречитатьДополнительныеОграниченияСитиГИД()

&НаСервере
Процедура ВыполнитьЗаписьОграниченийСитиГИД()
	
	Если НаборКонстант.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД Тогда
		ПараметрыМаршрутизацииСитиГИД = Константы.упПараметрыМаршрутизацииСитиГИД.Получить();
		ТекущаяСтруктура = ПараметрыМаршрутизацииСитиГИД.Получить();
		Если ТипЗнч(ТекущаяСтруктура) = ТИП("Структура")Тогда
			ТекущаяСтруктура.Вставить("ДополнительныеОграниченияСитиГИД",ДополнительныеОграниченияСитиГИД.Выгрузить());
			ХранилищеЗначения = Новый ХранилищеЗначения(ТекущаяСтруктура);					
			Константы.упПараметрыМаршрутизацииСитиГИД.Установить(ХранилищеЗначения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьЗаписьОграниченийСитиГИД()

&НаСервере
Функция ВыполнимПроверкуИзмененияОграничений()
	
	Результат = Ложь;
	
	Если НаборКонстант.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД Тогда
		ПараметрыМаршрутизацииСитиГИД = Константы.упПараметрыМаршрутизацииСитиГИД.Получить();
		ТекущаяСтруктура = ПараметрыМаршрутизацииСитиГИД.Получить();	
		ТаблицаОграничений = Неопределено;
		Если ТипЗнч(ТекущаяСтруктура) = ТИП("Структура")Тогда
			Если ТекущаяСтруктура.Свойство("ДополнительныеОграниченияСитиГИД") Тогда
				ТаблицаОграничений = ТекущаяСтруктура.ДополнительныеОграниченияСитиГИД;	
			КонецЕсли;
		КонецЕсли;
		Если НЕ ТаблицаОграничений = Неопределено Тогда
			Для каждого ТекущийПропуск Из ТаблицаОграничений Цикл
				ТекущийОтбор = Новый Структура("Ограничение",ТекущийПропуск.Ограничение);
				НайденныеСтроки = ДополнительныеОграниченияСитиГИД.НайтиСтроки(ТекущийОтбор);
				Если НайденныеСтроки.Количество() Тогда
					Если НЕ НайденныеСтроки[0].Пометка = ТекущийПропуск.Пометка Тогда
						Результат = Истина;
						Прервать;
					КонецЕсли;
				Иначе
					Результат = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВыполнимПроверкуИзмененияОграничений()

&НаКлиенте
Функция ЛегальностьИспользованияСервисаAxelotMaps(ИмяОбъекта)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПрограммноеОткрытие", Истина);
	ПараметрыФормы.Вставить("ПоказыватьПредупреждениеОПерезапуске", Ложь);
	ПараметрыФормы.Вставить("ПропуститьПерезапуск", Истина);
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Отказ", Ложь);
	ПараметрыОповещения.Вставить("Значение", ТекущееЗначениеЭлемента);
	ПараметрыОповещения.Вставить("ИмяЭлемента", ИмяОбъекта);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаAxelotMaps",ЭтотОбъект, ПараметрыОповещения);
	ОткрытьФорму("Обработка.упЛегальностьИспользованияСервисаAxelotMaps.Форма", ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно,,ОповещениеОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции // ЛегальностьИспользованияСервисаAxelotMaps()

&НаКлиенте
Функция ЛегальностьИспользованияСервиса(ИмяОбъекта)
	
	ЗначениеСервиса = НаборКонстант[ИмяОбъекта];
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПрограммноеОткрытие", Истина);
	ПараметрыФормы.Вставить("ПоказыватьПредупреждениеОПерезапуске", Ложь);
	ПараметрыФормы.Вставить("ПропуститьПерезапуск", Истина);
	ПараметрыФормы.Вставить("ИмяЭлемента", ИмяОбъекта);
	ПараметрыФормы.Вставить("Значение", ЗначениеСервиса);
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Отказ", Ложь);
	ПараметрыОповещения.Вставить("Значение", ТекущееЗначениеЭлемента);
	ПараметрыОповещения.Вставить("ИмяЭлемента", ИмяОбъекта);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервиса", ЭтотОбъект, ПараметрыОповещения);
	ОткрытьФорму("Обработка.упЛегальностьИспользованияСервиса.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор, ВариантОткрытияОкна.ОтдельноеОкно,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции // ЛегальностьИспользованияСервиса()

&НаКлиенте
Функция ЛегальностьИспользованияСервисаЯндекс(ИмяОбъекта)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПрограммноеОткрытие", Истина);
	ПараметрыФормы.Вставить("ПоказыватьПредупреждениеОПерезапуске", Ложь);
	ПараметрыФормы.Вставить("ПропуститьПерезапуск", Истина);
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Отказ", Ложь);
	ПараметрыОповещения.Вставить("Значение", ТекущееЗначениеЭлемента);
	ПараметрыОповещения.Вставить("ИмяЭлемента", ИмяОбъекта);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаЯндекс",ЭтотОбъект, ПараметрыОповещения);
	ОткрытьФорму("Обработка.упЛегальностьИспользованияСервисаЯндекс.Форма", ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно,,ОповещениеОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции // ЛегальностьИспользованияСервисаЯндекс()

&НаКлиенте
Процедура ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервиса(Результат, ПараметрыОповещения) Экспорт
	
	Если НЕ Результат = Истина Тогда
		ПараметрыОповещения.Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыОповещения.Свойство("Отказ") И ПараметрыОповещения.Отказ = Истина Тогда		
		Модифицированность = Истина;
		НаборКонстант[ПараметрыОповещения.ИмяЭлемента] = ПараметрыОповещения.Значение;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение отменено.'"),,НСтр("ru = 'Легальность использования Сервиса.'"), БиблиотекаКартинок.Информация32);
	Иначе		
		
		ВыбратьОтображениеСервиса(ПараметрыОповещения.Значение);
		
		Если ТипЗнч(ПараметрыОповещения.Значение) = ТИП("ПеречислениеСсылка.упТипыКарт") Тогда		
			Если НЕ ПустаяСтрока(НаборКонстант.упКлючЭлектронныхГеографическихКарт) Тогда
				ТекущийЭлемент = Элементы.упКлючЭлектронныхГеографическихКарт;
				ТекстВопроса = НСтр("ru = 'Значение указанного ключа заполнено, очистить его?'");
				ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);
				ПоказатьВопрос(ОписаниеОповещения,ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Очистить заполненое поле");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПриИзмененииТипМаршрутизатора();
	
КонецПроцедуры // ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервиса()

&НаКлиенте
Процедура ВыбратьОтображениеСервиса(ЗначениеСервиса)
	
	Если ТипЗнч(ЗначениеСервиса) = ТИП("ПеречислениеСсылка.упТипыМаршрутизаторов") Тогда
		УстановитьВидимостьАдресаСервераМаршрутизации();
	ИначеЕсли ТипЗнч(ЗначениеСервиса) = ТИП("ПеречислениеСсылка.упТипыКарт") Тогда
		УстановитьДоступностьОбщая();
		УстановитьДоступностьКарт();
	ИначеЕсли ТипЗнч(ЗначениеСервиса) = ТИП("ПеречислениеСсылка.упТипыГеокодеров") Тогда
		УстановитьДоступностьГеокодера();
	Иначе
		УстановитьДоступность();
	КонецЕсли;
	
КонецПроцедуры // ВыбратьОтображениеСервиса()

&НаКлиенте
Процедура ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаЯндекс(Результат, ПараметрыОповещения) Экспорт
	
	Если НЕ Результат = Истина Тогда
		ПараметрыОповещения.Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыОповещения.Свойство("Отказ") И ПараметрыОповещения.Отказ = Истина Тогда		
		Модифицированность = Истина;
		НаборКонстант[ПараметрыОповещения.ИмяЭлемента] = ПараметрыОповещения.Значение;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение отменено.'"),,НСтр("ru = 'Легальность использования Яндекс Сервиса.'"), БиблиотекаКартинок.Информация32);
	Иначе
		ВыбратьОтображениеСервиса(ПараметрыОповещения.Значение);
	КонецЕсли;
	
	ПриИзмененииТипМаршрутизатора();
	
КонецПроцедуры // ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаЯндекс()

&НаКлиенте
Процедура ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаAxelotMaps(Результат, ПараметрыОповещения) Экспорт
	
	Если НЕ Результат = Истина Тогда
		ПараметрыОповещения.Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыОповещения.Свойство("Отказ") И ПараметрыОповещения.Отказ = Истина Тогда
		Модифицированность = Истина;
		НаборКонстант[ПараметрыОповещения.ИмяЭлемента] = ПараметрыОповещения.Значение;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение отменено.'"),,НСтр("ru = 'Легальность использования сервиса AxelotMaps.'"), БиблиотекаКартинок.Информация32);
	Иначе
		ВыбратьОтображениеСервиса(ПараметрыОповещения.Значение);
	КонецЕсли;
	
	ПриИзмененииТипМаршрутизатора();
	
КонецПроцедуры // ПослеЗакрытияФормыПроверкиЛегальностьИспользованияСервисаAxelotMaps()

&НаКлиенте
Процедура УстановитьДоступностьОбщая()
	
	Элементы.упКоличествоПотоковРасчета.Доступность             = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;
	Элементы.ГруппаНастройкиРасчетаБлизкихТочек.Доступность     = НаборКонстант.упРассчитыватьРасстоянияТолькоМеждуГеографическиБлизкимиТочками;
	Элементы.упРазмерПорцииРассчитываемыхДанных.Доступность     = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;
	Элементы.упПериодАктуальностиРасчета.Доступность            = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;
	Элементы.грпТипыКарт.Доступность                            = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;
	Элементы.грпТипМаршрутизации.Доступность                    = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;
	Элементы.упТипКарт.АвтоОтметкаНезаполненного                = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;  
	Элементы.упТипМаршрутизатора.АвтоОтметкаНезаполненного      = НаборКонстант.упИспользуетсяПостроительМаршрутов;  
	Элементы.упТипГеокодера.АвтоОтметкаНезаполненного           = НаборКонстант.упИспользуетсяГеокодированиеАдресов;  
	Элементы.упКлючЭлектронныхГеографическихКарт.Видимость      = НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты;  
	Элементы.грпГеокодер.Доступность                            = НаборКонстант.упИспользуетсяГеокодированиеАдресов;
	
	Если НаборКонстант.упИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Элементы.грпТипМаршрутизации.Доступность = НаборКонстант.упИспользуетсяПостроительМаршрутов;
	КонецЕсли;

КонецПроцедуры // УстановитьДоступностьОбщая()

&НаКлиенте
Процедура УстановитьДоступностьКарт()
	
	упАдресСервераЭлектронныхКартВидимость = Ложь;
	Если НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.OSM") Тогда
		упАдресСервераЭлектронныхКартВидимость = Истина;
		Элементы.упАдресСервераЭлектронныхКарт.ПодсказкаВвода = "http://{s}.tile.openstreetmap.org";
		СписокЗначений = Элементы.упАдресСервераЭлектронныхКарт.СписокВыбора;
		СписокЗначений.ЗагрузитьЗначения(АдресаСерверовЭлектронныхКарт.ВыгрузитьЗначения());
	КонецЕсли;
	
	Если НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.СитиГИД") Тогда
		упАдресСервераЭлектронныхКартВидимость = Истина;
		СписокЗначений = Элементы.упАдресСервераЭлектронныхКарт.СписокВыбора;
		Элементы.упАдресСервераЭлектронныхКарт.ПодсказкаВвода = "https://api.probki.net/map";
		СписокЗначений.Очистить();
	КонецЕсли;
	Элементы.упАдресСервераЭлектронныхКарт.Видимость = упАдресСервераЭлектронныхКартВидимость;
	
	Если НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps") Тогда
		Элементы.грпНадпись.Видимость = Истина;
		Элементы.НадписьПро.Заголовок    = НСтр("ru = 'Доступ предоставляется компанией ""Axelot"" для ознакомления пользователей с возможностями сервиса картографии. Использование сервиса в промышленной эксплуатации запрещено.'");
		Элементы.НадписьСсылка.Заголовок = "";
	    Элементы.упКлючЭлектронныхГеографическихКарт.Видимость      = Ложь; 
		
	ИначеЕсли НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.GoogleMaps") Тогда
		Элементы.грпНадпись.Видимость = Истина;
		Элементы.НадписьПро.Заголовок    = НСтр("ru = 'Использование карт Google Maps  возможно только при приобретении ключа API Google Карт'");
		Элементы.НадписьСсылка.Заголовок = "https://developers.google.com/maps/licensing?hl=ru";
		
	ИначеЕсли НаборКонстант.упТипКарт = ПредопределенноеЗначение("Перечисление.упТипыКарт.ЯндексКарты") Тогда
		Элементы.грпНадпись.Видимость = Истина;
		Элементы.НадписьПро.Заголовок    = НСтр("ru = 'Использование Яндекс Карт, возможно только при приобретении ключа API Яндекс.Карт'");
		Элементы.НадписьСсылка.Заголовок = "https://tech.yandex.ru/maps/commercial/";
		
	Иначе	
		Элементы.грпНадпись.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКарт()

&НаКлиенте
Процедура УстановитьДоступностьГеокодера()
	
	Если НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Nominatim")
		ИЛИ НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.AxelotMaps") Тогда
		Элементы.упАдресСервераГеокодированияNominatim.Видимость = Ложь;
		Если НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Nominatim") Тогда
			Элементы.упАдресСервераГеокодированияNominatim.Видимость = Истина;
		КонецЕсли;			
		Элементы.упКлючСервисаГеоКодирования.Видимость = Ложь;
	Иначе
		Элементы.упАдресСервераГеокодированияNominatim.Видимость = Ложь;
		Элементы.упКлючСервисаГеоКодирования.Видимость = Истина;
	КонецЕсли;	
		
	Если НаборКонстант.упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.СитиГИД") Тогда
		Элементы.упАдресСервераГеокодирования.Видимость = Истина;
		Элементы.упАдресСервераГеокодированияСитиГИД.Видимость = Истина;
	Иначе
		Элементы.упАдресСервераГеокодирования.Видимость = Ложь;
		Элементы.упАдресСервераГеокодированияСитиГИД.Видимость = Ложь;
	КонецЕсли;
	Элементы.ГруппаСервисDaData.Доступность = НаборКонстант.упНормализоватьАдресаСервисомDaData;
	
КонецПроцедуры // УстановитьДоступностьМаршрутизатора()

&НаКлиенте
Процедура УстановитьВидимостьАдресаСервераМаршрутизации()

	грпСервисМаршрутизацииGraphhopperВидимость = Ложь;
	упКлючСервисаМаршрутизацииВидимость = Ложь;	
	ДополнительныйПараметрыЗапросаМаршрутизацииВидимость = Ложь;
	упКлючДоступаСервисаМаршрутизацииСитиГИДВидимость = Ложь;
	упАдресСервераМаршрутизацииСитиГИДВидимость = Ложь;
	упРесурсСитиГИДВидимость = Ложь;
	ГруппаМаршрутизацияЯндексВидимость = Ложь;
	ГруппаЯндексМаршрутизацияВидимость = Ложь;
	ГруппаОграниченияСитиГИДВидимость = Ложь;
	//упРассчитыватьВремяПоДнямНеделиСитиГИДВидимость = Ложь;
	ПрименитьКлючМаршрутизацииВидимость = Ложь;
	упПакетныйРежимОтправкиЗапросаСервисуВидимость	= Ложь;
	упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИДВидимость = Ложь;
	
	Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексКарты") Тогда
		ГруппаМаршрутизацияЯндексВидимость = Истина;
		ПрименитьКлючМаршрутизацииВидимость = Истина;
	КонецЕсли;
	Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.ЯндексМаршрутизация") Тогда
		ГруппаЯндексМаршрутизацияВидимость = Истина;
		ДополнительныйПараметрыЗапросаМаршрутизацииВидимость = Истина;
	КонецЕсли;
	Если НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.Graphhopper") Тогда
		грпСервисМаршрутизацииGraphhopperВидимость = Истина;
		ДополнительныйПараметрыЗапросаМаршрутизацииВидимость = Истина;
	ИначеЕсли НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.СитиГИД") Тогда
		упКлючДоступаСервисаМаршрутизацииСитиГИДВидимость = Истина;
		упАдресСервераМаршрутизацииСитиГИДВидимость = Истина;
		упРесурсСитиГИДВидимость = Истина;
		упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИДВидимость = Истина;
		ГруппаОграниченияСитиГИДВидимость = Истина;
		упПакетныйРежимОтправкиЗапросаСервисуВидимость = Истина;
		//упРассчитыватьВремяПоДнямНеделиСитиГИДВидимость = Истина;
		Элементы.ДополнительныеОграниченияСитиГИД.ТолькоПросмотр = Не НаборКонстант.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД;
	ИначеЕсли Не НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.AxelotMaps") Тогда
		ДополнительныйПараметрыЗапросаМаршрутизацииВидимость = Истина;
		упКлючСервисаМаршрутизацииВидимость = Истина;
	КонецЕсли;
	Элементы.упУчитыватьКоэффициентыРасчетаВремениВПути.Видимость = Ложь
		Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.Graphhopper")
		Или НаборКонстант.упТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.AxelotMaps");
		
	Элементы.грпСервисМаршрутизацииGraphhopper.Видимость = грпСервисМаршрутизацииGraphhopperВидимость;
	Элементы.упКлючСервисаМаршрутизации.Видимость = упКлючСервисаМаршрутизацииВидимость;	
	Элементы.ДополнительныйПараметрыЗапросаМаршрутизации.Видимость = упКлючСервисаМаршрутизацииВидимость;
	Элементы.упКлючДоступаСервисаМаршрутизацииСитиГИД.Видимость = упКлючДоступаСервисаМаршрутизацииСитиГИДВидимость;
	Элементы.упАдресСервераМаршрутизацииСитиГИД.Видимость = упКлючДоступаСервисаМаршрутизацииСитиГИДВидимость;
	Элементы.упРесурсСитиГИД.Видимость = упРесурсСитиГИДВидимость;
	Элементы.ГруппаМаршрутизацияЯндекс.Видимость = ГруппаМаршрутизацияЯндексВидимость;
	Элементы.ГруппаЯндексМаршрутизация.Видимость = ГруппаЯндексМаршрутизацияВидимость;
	Элементы.ГруппаОграниченияСитиГИД.Видимость = ГруппаОграниченияСитиГИДВидимость;
	//Элементы.упРассчитыватьВремяПоДнямНеделиСитиГИД.Видимость = упРассчитыватьВремяПоДнямНеделиСитиГИДВидимость;
	Элементы.ПрименитьКлючМаршрутизации.Видимость = ПрименитьКлючМаршрутизацииВидимость;
	Элементы.упПакетныйРежимОтправкиЗапросаСервису.Видимость = упПакетныйРежимОтправкиЗапросаСервисуВидимость;
	Элементы.упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИД.Видимость = упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИДВидимость;
	
	Элементы.упПрямойРасчетГеографическиБлизкихТочекПриНеудаче.Видимость = НаборКонстант.упТипМаршрутизатора <> ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.GoogleMaps"); 
	Элементы.ГруппаКритерийБлизостиТочек.Видимость = НаборКонстант.упТипМаршрутизатора <> ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.GoogleMaps"); 
КонецПроцедуры // УстановитьВидимостьАдресаСервераМаршрутизации()

&НаСервереБезКонтекста
Функция ВыполнитьПроверкуСервисаМаршрутизации(ПараметрыПроверки)

	Возврат упМаршрутизация.ВыполнитьПровекруСервисаМаршрутизации(ПараметрыПроверки);	

КонецФункции // ВыполнитьПроверкуСервисаМаршрутизации()

&НаСервереБезКонтекста
Функция ВыполнитьОтправкуКлючаСервисаМаршрутизации(ПараметрыОтправки)

	Возврат упМаршрутизация.ВыполнитьОтправкуКлючаСервисаМаршрутизации(ПараметрыОтправки);	

КонецФункции // ВыполнитьОтправкуКлючаСервисаМаршрутизации()

&НаКлиенте
Процедура упНормализоватьАдресаСервисомDadataПриИзменении(Элемент)
	
	ЭтаФорма.ТекущееЗначениеЭлемента = Не НаборКонстант.упНормализоватьАдресаСервисомDadata;
	Если Не ТекущееЗначениеЭлемента Тогда
		ЛегальностьИспользованияСервиса(Элемент.Имя);
	Иначе	
		НаборКонстант.упAPIКлючСервисаDaData = "";
		НаборКонстант.упСекретныйКлючСервисаDaData = "";	
	КонецЕсли; 
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упКаталогВнешнихОбработокДляОтладкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ВыборФайла.Каталог = НаборКонстант.упКаталогВнешнихОбработокДляОтладки;
	Если Не ВыборФайла.Выбрать() Тогда
		Возврат;		
	КонецЕсли;
	НаборКонстант.упКаталогВнешнихОбработокДляОтладки = ВыборФайла.Каталог;
	ЭтаФорма.Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура упВедетсяВалютныйУчетПриИзменении(Элемент)

	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упУказываютсяФактическиОказанныеУслугиПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура упУчитыватьРежимТрудаИОтдыхаПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуютсяУслугиИнкассацииПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяАндроидКлиентПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упСтатистикаНахождениеВТочкахПартнеровУчитыватьПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура упСтатистикаНахождениеВТочкахПартнеровПериодАнализаПриИзменении(Элемент)
	
	Если НаборКонстант.упСтатистикаНахождениеВТочкахПартнеровПериодАнализа = 0 Тогда
		ТекстСообщения = Нстр("ru = 'Период анализа не задан. Будет использоваться весь период работы.'");
		ПоказатьПредупреждение(,ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяИнтеграцияСPacker3DПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Истина
		И НаборКонстант.упИспользуетсяУчетМатериалов 
		И Не ЗначениеЗаполнено(НаборКонстант.упМетодОценкиСтоимостиМатериалов) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите метод оценки стоимости материалов!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упМетодОценкиСтоимостиМатериалов","НаборКонстант", Отказ);
	КонецЕсли; 
    
    Если Истина
	    И НаборКонстант.упВедетсяВалютныйУчет
	    И Не ЗначениеЗаполнено(НаборКонстант.упВалютаУправленческогоУчета) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Валюта управленческого учета"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упВалютаУправленческогоУчета","НаборКонстант",Отказ);
    КонецЕсли;
		
	Если Истина
	    И НаборКонстант.упИспользуетсяАндроидКлиент
	    И Не ЗначениеЗаполнено(НаборКонстант.упВерсияМобильногоКлиента)
	Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Версия мобильного клиента"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упВерсияМобильногоКлиента","НаборКонстант",Отказ);
    КонецЕсли;
	
    Если Истина
	    И НаборКонстант.упУчитыватьЛинейныеРазмеры
	    И Не ЗначениеЗаполнено(НаборКонстант.упЕдиницаИзмеренияЛинейныхГабаритов) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Единица измерения линейных габаритов"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упЕдиницаИзмеренияЛинейныхГабаритов","НаборКонстант",Отказ);
    КонецЕсли;
    
    Если Истина
	    И НаборКонстант.упУчитыватьМассу
	    И Не ЗначениеЗаполнено(НаборКонстант.упЕдиницаИзмеренияМассы) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Единица измерения массы"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упЕдиницаИзмеренияМассы","НаборКонстант",Отказ);
    КонецЕсли;
    
    Если Истина
	    И НаборКонстант.упУчитыватьОбъем
	    И Не ЗначениеЗаполнено(НаборКонстант.упЕдиницаИзмеренияОбъема) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Единица измерения объема"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упЕдиницаИзмеренияОбъема","НаборКонстант",Отказ);
    КонецЕсли;

    Если Истина
	    И НаборКонстант.упУчитыватьКоличествоМест
	    И Не ЗначениеЗаполнено(НаборКонстант.упЕдиницаИзмеренияГрузовыхМест) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Единица измерения грузовых мест"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упЕдиницаИзмеренияГрузовыхМест","НаборКонстант",Отказ);
    КонецЕсли;
    
     Если Истина
	    И НаборКонстант.упПрямойРасчетГеографическиБлизкихТочекПриНеудаче
	    И Не ЗначениеЗаполнено(НаборКонстант.упПорядокПрямогоРасчетаГеографическиБлизкихТочек) Тогда
	    ТекстСообщения = Нстр("ru = 'Поле ""Порядок прямого расчета географически близких точек"" не заполнено'");
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"упПорядокПрямогоРасчетаГеографическиБлизкихТочек","НаборКонстант",Отказ);
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяУчетГСМПриИзменении(Элемент)

	Если Не НаборКонстант.упИспользуетсяУчетГСМ Тогда 
		НаборКонстант.упИспользуетсяУчетПутевыхЛистов = Ложь;
		НаборКонстант.упРазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов = Ложь;
		НаборКонстант.упИспользуетсяЗагрузкаДанныхОтТопливныхКомпаний = Ложь;
		НаборКонстант.упИспользуютсяЛимитыРасходаГСМ = Ложь;
	КонецЕсли;	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяУчетМатериаловПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура упИспользуетсяУчетПутевыхЛистовПриИзменении(Элемент)
	Если Не НаборКонстант.упИспользуетсяУчетПутевыхЛистов Тогда 
		НаборКонстант.упВестиНумерациюПутевыхЛистовВРазрезеОрганизаций = Ложь;
		НаборКонстант.упРазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов = Ложь;
	КонецЕсли;	
	УстановитьДоступность();
КонецПроцедуры

&НаСервере 
Процедура ОтобразитьСостояниеИспользованияКомпонент()
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом") Тогда
		Элементы.КартинкаУТ.Картинка = БиблиотекаКартинок.упУправлениеТранспортомВкл;
		Элементы.СостояниеУТ.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	Иначе	
		Элементы.КартинкаУТ.Картинка = БиблиотекаКартинок.упУправлениеТранспортомВыкл;
		Элементы.СостояниеУТ.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли; 
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Элементы.КартинкаУП.Картинка = БиблиотекаКартинок.упУправлениеПеревозкамиВкл;
		Элементы.СостояниеУП.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
		Элементы.упКонтролироватьКоличествоПосадочныхМест.Доступность = Истина;
	Иначе	
		Элементы.КартинкаУП.Картинка = БиблиотекаКартинок.упУправлениеПеревозкамиВыкл;
		Элементы.СостояниеУП.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		Элементы.упКонтролироватьКоличествоПосадочныхМест.Доступность = Ложь;
	КонецЕсли; 
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг") Тогда
		Элементы.КартинкаСМ.Картинка = БиблиотекаКартинок.упСпутниковыйМониторингВкл;
		Элементы.СостояниеСМ.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	Иначе	
		Элементы.КартинкаСМ.Картинка = БиблиотекаКартинок.упСпутниковыйМониторингВыкл;
		Элементы.СостояниеСМ.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли; 
	
	// доступность подсистемы "Транспортные средства и планирование".
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом") И Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Элементы.ТранспортныеСредстваИПланирование.Доступность = Ложь;
	КонецЕсли;
	
	// доступность подсистемы "Учет ГСМ и управление транспортом".
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом") Тогда
		Элементы.УправлениеТранспортом.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбменДаннымиНажатие(Элемент)
	ОткрытьФорму("ОбщаяФорма.сшпФормаКонстант");
КонецПроцедуры

&НаКлиенте
Процедура упВестиОтдельныйУчетПоказанийОдометровТранспортныхСредствПриИзменении(Элемент)
	Если НаборКонстант.упВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств Тогда
		ЕстьОстаткиПоПробегуТС = ЕстьОстаткиПоПробегу();
		Если ЕстьОстаткиПоПробегуТС Тогда
			ТекстСообщения = Нстр("ru = 'В системе имеются остатки по пробегу транспортных средств. 
									|Для корректной работы, следует занести начальные остатки показаний одометров,
									|воспользовавшись документом '");
			СтрокаНовыйДокумент	= """Корректировка пробега ТС""";
			СсылкаНовыйДокумент = "e1cib/list/Документ.упКорректировкаПробегаТС";
			СтрокаНовыйДокументФорматированная = Новый ФорматированнаяСтрока(СтрокаНовыйДокумент,,,,СсылкаНовыйДокумент);
			СтрокаСообщение = Новый ФорматированнаяСтрока(ТекстСообщения, СтрокаНовыйДокументФорматированная);
			ПоказатьПредупреждение(,СтрокаСообщение);
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьОстаткиПоПробегу()
	Возврат упПутевойЛистУчетГСМ.ЕстьОстаткиПоПробегуТС();
КонецФункции

&НаКлиенте
Процедура упУказываютсяФактическиеРасходыПоРейсуПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура упПрямойРасчетГеографическиБлизкихТочекПриНеудачеПриИзменении(Элемент)
	Если НЕ НаборКонстант.упПрямойРасчетГеографическиБлизкихТочекПриНеудаче Тогда
		НаборКонстант.упКритерийБлизостиТочекВМетрах = 0;
	Иначе
		Если НЕ ЗначениеЗаполнено(НаборКонстант.упПорядокПрямогоРасчетаГеографическиБлизкихТочек) Тогда
			НаборКонстант.упПорядокПрямогоРасчетаГеографическиБлизкихТочек = ПредопределенноеЗначение("Перечисление.упПорядокПрямогоРасчетаГеографическиБлизкихТочек.ПриНеудачеМаршрутизатора");	
		КонецЕсли;
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура КонсольСервераЗащиты(Команда)
	
	ОткрытьФорму("ОбщаяФорма.упКонсольСервераЗащиты",, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
