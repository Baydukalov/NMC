#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	спзДокументы = Новый Массив;
	Для каждого Элемент Из Параметры.мсвДокументы Цикл
		спзДокументы.Добавить(Элемент);
	КонецЦикла;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПерепрефиксацию(Команда)
	ВыполнитьПерепрефиксациюНаСервере();
	Закрыть();
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ВыполнитьПерепрефиксациюНаСервере()
	
	КоличествоДокументовВсего = спзДокументы.Количество();
	
	Если КоличествоДокументовВсего > 0 Тогда
		МетаданныеДокумента 	= спзДокументы.Получить(0).Значение.Получитьобъект().Метаданные();	
		ДлинаНомераДокумента 	= МетаданныеДокумента.ДлинаНомера;
		ДлинаПрефиксаОР			  = ДлинаПрефиксаОрганизации();
		
		СобытиеНачало	= Нстр("ru = 'ВыполнениеПерепрефиксацииОбъектов.Начало'");
		СобытиеКонец 	= Нстр("ru = 'ВыполнениеПерепрефиксацииОбъектов.Конец'");
		
		Этап	= Нстр("ru = 'Начало перепрефиксации объектов'");
		ЗаписьЖурналаРегистрации(СобытиеНачало, УровеньЖурналаРегистрации.Информация,,, Этап);	
		
		КоличествоДокументовУспешно 	= 0;
		КоличествоДокументовНовыйНомер 	= 0;
		КоличествоДокументовНеУдалосьНазначитьНомер = 0;
				
		Для каждого ЭлементСписка Из спзДокументы Цикл
			
			ДокументСсылка = ЭлементСписка.Значение;
			
			НомерДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Номер");
			
			НормерСимволаПрефикса = СтрНайти(НомерДокумента, "-", НаправлениеПоиска.СКонца);
			
			Если НормерСимволаПрефикса > 1 Тогда
				ЦифроваяЧастьНомера = Прав(НомерДокумента, СтрДлина(НомерДокумента)-НормерСимволаПрефикса);
			Иначе
				ЦифроваяЧастьНомера = НомерДокумента;	
			КонецЕсли;
		
			ЦифроваяЧастьНомера = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ЦифроваяЧастьНомера, "0");
	
			НовыйПрефиксЛк 		= Сокрлп(НовыйПрефикс);
			ДлинаНовогоПрефикса = СтрДлина(НовыйПрефиксЛк);
			НовыйПрефиксЛк 		= СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НовыйПрефиксЛк, ДлинаПрефиксаОР, "0", "Слева");
						
			Отказ = Ложь;
			
			Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЦифроваяЧастьНомера) Тогда
				
				// Неизвестный формат номера
				Событие 		= Нстр("ru = 'Не удалось записать документ'");
				ОписаниеОшибки 	= Нстр("ru = 'Не удалось определить формат номера ""%1"". Номер должен быть в формате: [ПРЕФИКС ОБЪЕКТА-]ЦИФРОВАЯ ЧАСТЬ НОМЕРА.'");
				ОписаниеОшибки 	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, НомерДокумента);
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,ДокументСсылка, ОписаниеОшибки); 
				Продолжить;
				
			КонецЕсли;
			
			КоличествоСимволовПодПрефикс = ДлинаПрефиксаОР + 1;
			КоличествоСимволовПодНомер   = ДлинаНомераДокумента - КоличествоСимволовПодПрефикс;
			
			Если  СтрДлина(ЦифроваяЧастьНомера) > КоличествоСимволовПодНомер Тогда
				Отказ = Истина;
				// Неизвестный формат номера
				Событие = Нстр("ru = 'Не удалось установить префикс'");
				ОписаниеОшибки = Нстр("ru = 'Цифровая часть номера слишком длинная:%1'");
				ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ЦифроваяЧастьНомера);
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,ДокументСсылка, ОписаниеОшибки); 
				Если НЕ НазначатьНовыйНомер Тогда
					Продолжить;	
				КонецЕсли;
			КонецЕсли;
			
			ДокументОбъект = Неопределено;
			
			Если НЕ Отказ Тогда
				Если ДлинаНовогоПрефикса > 0 Тогда
					ШаблонПрефикса = "[Префикс]-";
					ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[Префикс]", НовыйПрефиксЛк);	
					НовыйНомерДокумента = ШаблонПрефикса + СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЦифроваяЧастьНомера, КоличествоСимволовПодНомер, "0", "Слева");
				Иначе
					НовыйНомерДокумента = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЦифроваяЧастьНомера, ДлинаНомераДокумента, "0", "Слева");
				КонецЕсли;
				
				ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
				ДокументОбъект.ДополнительныеСвойства.Вставить("НеУстанавливатьПрефиксОрганизации");
				ДокументОбъект.Номер = НовыйНомерДокумента;
				
				Отказ = Ложь;				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					КоличествоДокументовУспешно = КоличествоДокументовУспешно + 1;
				Исключение
					Отказ = Истина;
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
						ОписаниеОшибки = ОписаниеОшибки();
					КонецЕсли;
					Событие = Нстр("ru = 'Не удалось записать документ'");
					ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,ДокументОбъект, ОписаниеОшибки); 
					Если НЕ НазначатьНовыйНомер Тогда
						КоличествоДокументовНеУдалосьНазначитьНомер = КоличествоДокументовНеУдалосьНазначитьНомер + 1;
					КонецЕсли;
				КонецПопытки;
			КонецЕсли;	
				
			Если Отказ И НазначатьНовыйНомер Тогда
				Если ДлинаНовогоПрефикса = 0 Тогда
					НовыйПрефиксЛк 	= Лев(НовыйНомерДокумента, СтрДлина(НовыйНомерДокумента) - СтрДлина(ЦифроваяЧастьНомера));
				Иначе
					НовыйПрефиксЛк 	= НовыйПрефиксЛк + "-";
				КонецЕсли;
				
				Если ДокументОбъект = Неопределено Тогда
					ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
					ДокументОбъект.ДополнительныеСвойства.Вставить("НеУстанавливатьПрефиксОрганизации");
				КонецЕсли;
				
				ДокументОбъект.УстановитьНовыйНомер(НовыйПрефиксЛк);		
				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					КоличествоДокументовНовыйНомер = КоличествоДокументовНовыйНомер + 1;
				Исключение
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
						ОписаниеОшибки = ОписаниеОшибки();
					КонецЕсли;
					Событие = Нстр("ru = 'Не удалось установить новый номер'");
					ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,ДокументОбъект, ОписаниеОшибки); 
					КоличествоДокументовНеУдалосьНазначитьНомер = КоличествоДокументовНеУдалосьНазначитьНомер + 1;
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		
		Комментарий = Нстр("ru = 'Всего обработано %1 документов, из них: успешно изменен префикс - %2, назначен новый номер - %3, не удалось назначить новый номер - %4'");
		Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Комментарий, КоличествоДокументовВсего,КоличествоДокументовУспешно, КоличествоДокументовНовыйНомер, КоличествоДокументовНеУдалосьНазначитьНомер); 
		ЗаписьЖурналаРегистрации(СобытиеКонец, УровеньЖурналаРегистрации.Информация,,, Комментарий);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДлинаПрефиксаОрганизации() Экспорт
	Возврат 2;
КонецФункции


#КонецОбласти
