
#Область ПеременныеМодуляФормы

// Переменная ЧислоПопыток хранит количество попыток сформировать документ HTML.
// Переменнная ЧислоЗагрузок хранит количество сформированных документов HTML.
&НаКлиенте
Перем ЧислоПопыток, ЧислоЗагрузок, MapСформирован;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		упТипКарт = Параметры.упТипКарт;
		КлючAPI = Параметры.упКлючЭлектронныхГеографическихКарт;
		упАдресСервераЭлектронныхКарт = Параметры.упАдресСервераЭлектронныхКарт;
		HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартографическогоСервисаСПараметрами(упАдресСервераЭлектронныхКарт, КлючAPI, упТипКарт, Истина);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,Нстр("ru = 'Проверка работы карты в веб-клиенте не поддерживается.'"));
	#Иначе
		
		MapСформирован = Ложь; // карта не сформирована.
		
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "ratio = 0.6; //%pologenie ukazatelj%","ratio = 1;");
		
		НачальнаяШирота  = 55.84128;
		НачальнаяДолгота = 37.6495;
		НачальныйЗум     = 13;
		
		ЧислоПопыток  = 0;
		ЧислоЗагрузок = 0;
		
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%",     ПредставлениеЧисла(НачальнаяШирота));
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%",    ПредставлениеЧисла(НачальнаяДолгота));
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));
		
		сткКартинок = Новый Структура();
		сткКартинок.Вставить("упМаркерПростойБелый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойБелый, Неопределено));
		сткКартинок.Вставить("упМаркерПростойЖелтый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЖелтый, Неопределено));
		сткКартинок.Вставить("упМаркерПростойКрасный", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойКрасный, Неопределено));
		сткКартинок.Вставить("упГеокодируемыйМаркер", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упГеокодируемыйМаркер, Неопределено));
		сткКартинок.Вставить("упМаркерПростойЗеленый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЗеленый, Неопределено));
		сткКартинок.Вставить("упТумблер", 	            Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упТумблер, Неопределено));
		
		упСервисныеФункцииВызовСервера.ЗаполнитьДвоичныеДанныеКартинок(сткКартинок);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упТумблер.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойБелый.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЖелтый.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойКрасный.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упГеокодируемыйМаркер.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЗеленый.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерДвиженияТС%", ИмяВременногоФайла);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура кмдЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
	ЧислоПопыток = ЧислоПопыток + 1;
	
	// проверяет на стороне карты сформирован объект Map.
	MapСформирован = ВыполнитьФункциюHTML("ValidateCacheCalls()");	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьЗакрытиеНаВебКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	Закрыть();
	
КонецПроцедуры // ОбработатьЗакрытиеНаВебКлиенте()

&НаКлиентеНаСервереБезКонтекста
// Получить координаты строкой в формате.
//
// Параметры:
//  Координата - Число - Прдеставление координаты.
//
// Возвращаемое значение:
//  Строка - координата.
//
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции //ПредставлениеЧисла()

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Произвольное - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Попытка
		Возврат Элементы.HTMLДокумент.Документ.parentWindow.eval(текстСкрипта);
	Исключение
		Если ТипЗнч(MapСформирован)=ТИП("Булево") И MapСформирован = Истина Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),, "HTMLДокумент");
		КонецЕсли;
		Возврат "";
	КонецПопытки;	
	
КонецФункции //ВыполнитьФункциюHTML()

#КонецОбласти 