
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ДатаОстатков) Тогда
		ДатаОстатков = Параметры.ДатаОстатков
	Иначе
		ДатаОстатков = ТекущаяДатаСеанса();
	КонецЕсли; 
	
	ТипОборудования = Неопределено;
	
	Если НЕ Параметры.Свойство("ТипОборудования", ТипОборудования) Тогда
		ТипОборудования = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатов, "ЭтоАгрегат", ТипОборудования = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка(), Истина); 
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатовБезТС, "ЭтоАгрегат", ТипОборудования = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка(), Истина); 
	
	Если Параметры.Свойство("ТипДокумента") Тогда
		ТипДокумента = Параметры.ТипДокумента;
		Если НЕ ТипДокумента = ТИП("ДокументСсылка.упПоступлениеАгрегатов") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатов, "Период", Новый Граница(ДатаОстатков, ВидГраницы.Включая), Истина); 
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатовБезТС, "Период", Новый Граница(ДатаОстатков, ВидГраницы.Включая), Истина); 
		КонецЕсли;
	КонецЕсли;
	                         
	Если Параметры.Свойство("ТранспортноеСредство") Тогда
		ТранспортноеСредство = Параметры.ТранспортноеСредство;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатов, "ТС", ТранспортноеСредство, Истина); 
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатов, "ТС", Справочники.упТранспортныеСредства.ПустаяСсылка(), Истина); 
	КонецЕсли;
	
	Если Параметры.Свойство("МодельТС") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатовБезТС, "МодельТС", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МодельТС,"Модель"), Истина); 
	КонецЕсли;
	
	Если Параметры.Свойство("Состояние") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОстаткиАгрегатов, "Состояние", Параметры.Состояние, ВидСравненияКомпоновкиДанных.ВСписке);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОстаткиАгрегатовБезТС, "Состояние", Параметры.Состояние, ВидСравненияКомпоновкиДанных.ВСписке);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокАгрегаты, "Состояние", Параметры.Состояние, ВидСравненияКомпоновкиДанных.ВСписке);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокДопОборудование, "Состояние", Параметры.Состояние, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	Склад = ПредопределенноеЗначение("Справочник.упСклады.ПустаяСсылка");
	
	Если Параметры.Свойство("Склад") Тогда	
		Если ЗначениеЗаполнено(Параметры.Склад) Тогда
			Склад = Параметры.Склад;
		КонецЕсли;
	КонецЕсли;	
	
	Если Не Склад = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОстаткиАгрегатов, "Склад", Склад, Истина);
	КонецЕсли;
	
	ЗакрыватьПриВыбореАгрегата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗакрыватьПриВыбореАгрегата", Ложь);
		
	ОбновитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОстаткиАгрегатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ОстаткиАгрегатов.ДанныеСтроки(ВыбраннаяСтрока);
	сткПараметры = Новый Структура("МестоХранения, Агрегат, Количество, Тип, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(сткПараметры, текСтрока);
	
	ВводКоличестваЗавершение(сткПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиАгрегатовБезТСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ОстаткиАгрегатовБезТС.ДанныеСтроки(ВыбраннаяСтрока);
	сткПараметры = Новый Структура("МестоХранения, Агрегат, Количество, Тип, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(сткПараметры, текСтрока);
	
	ВводКоличестваЗавершение(сткПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокАгрегатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	текСтрока = Элементы.СписокАгрегаты.ДанныеСтроки(ВыбраннаяСтрока);
	сткПараметры = Новый Структура("МестоХранения, Агрегат, Количество, Тип, ЕдиницаИзмерения, СтатьяРасходов");
	ЗаполнитьЗначенияСвойств(сткПараметры, текСтрока);
	сткПараметры.Количество = 1;
	сткПараметры.МестоХранения = ПредопределенноеЗначение("Справочник.упСклады.ПустаяСсылка");
	
	ВводКоличестваЗавершение(сткПараметры);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	мсвРезультат = СформироватьВозвращаемоеЗначение();
	Закрыть(мсвРезультат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимость()	
	
	Элементы.ПодобранныеАгрегатыАгрегат.ТолькоПросмотр = Истина;
	Элементы.ПодобранныеАгрегатыЕдиницаИзмерения.ТолькоПросмотр = Истина;
	Элементы.ПодобранныеАгрегатыТип.ТолькоПросмотр = Истина;
	Элементы.ПодобранныеАгрегатыМестоХранения.ТолькоПросмотр = Истина;
	
	Если ТипЗнч(ТипОборудования) = Тип("СправочникСсылка.упДополнительноеОборудование") Тогда
		Элементы.грпНиз.Заголовок = НСтр("ru = 'Подобранное доп. оборудование'");
		Элементы.ПодобранныеАгрегатыАгрегат.Заголовок	= НСтр("ru = 'Доп. оборудование'");
		Элементы.ПодобранныеАгрегатыАгрегат.КартинкаШапки = БиблиотекаКартинок.упМонтаж;
		
		Элементы.ОстаткиАгрегатовБезТСАгрегат.Заголовок	= НСтр("ru = 'Доп. оборудование'");
		Элементы.ОстаткиАгрегатовБезТСАгрегат.КартинкаШапки = БиблиотекаКартинок.упМонтаж;

		Элементы.ОстаткиАгрегатовАгрегат.Заголовок	= НСтр("ru = 'Доп. оборудование'");
		Элементы.ОстаткиАгрегатовАгрегат.КартинкаШапки = БиблиотекаКартинок.упМонтаж;

	Иначе
		Элементы.грпНиз.Заголовок = НСтр("ru = 'Подобранные агрегаты'");
		Элементы.ПодобранныеАгрегатыАгрегат.Заголовок	= НСтр("ru = 'Агрегат'");
		Элементы.ПодобранныеАгрегатыАгрегат.КартинкаШапки = БиблиотекаКартинок.упАгрегаты;
		
		Элементы.ОстаткиАгрегатовБезТСАгрегат.Заголовок	= НСтр("ru = 'Агрегат'");
		Элементы.ОстаткиАгрегатовБезТСАгрегат.КартинкаШапки = БиблиотекаКартинок.упАгрегаты;
		
		Элементы.ОстаткиАгрегатовАгрегат.Заголовок	= НСтр("ru = 'Агрегат'");
		Элементы.ОстаткиАгрегатовАгрегат.КартинкаШапки = БиблиотекаКартинок.упАгрегаты;
		
	КонецЕсли;
	
	Если ТипДокумента = ТИП("ДокументСсылка.упПоступлениеАгрегатов") Тогда
		
		Если ТипЗнч(ТипОборудования) = Тип("СправочникСсылка.упДополнительноеОборудование") Тогда
			Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаДопОборудование.Видимость = Истина;
			Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаАгрегаты.Видимость = Ложь;
			ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Список доп.оборудования на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		Иначе	
			Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаАгрегаты.Видимость = Истина;
			Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаДопОборудование.Видимость = Ложь;
			ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Список агрегатов на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		КонецЕсли;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстатки.Видимость = Ложь;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстаткиБезТС.Видимость = Ложь;
		
	ИначеЕсли ТипДокумента = ТИП("ДокументСсылка.упРемонтИТОТС") и ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка() Тогда
		ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Остатки на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстатки.Видимость = Ложь;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаАгрегаты.Видимость = Ложь;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстаткиБезТС.Видимость = Истина;
		
	ИначеЕсли ТипДокумента = ТИП("ДокументСсылка.упКорректировкаОстатковАгрегатов") Тогда	
		
		Элементы.грпНиз.Видимость = Ложь;
		Элементы.ФормаПеренестиВДокумент.Видимость = Ложь;
		
		Если ТипЗнч(ТипОборудования) = Тип("СправочникСсылка.упДополнительноеОборудование") Тогда
			ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Список доп.оборудования на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		Иначе	
			ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Список агрегатов на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		КонецЕсли;
	
	Иначе
		
		ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Остатки на %1'"), Формат(ДатаОстатков, "ДФ='dd.MM.yyyy ЧЧ:mm:сс'"));
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстатки.Видимость = Истина;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаАгрегаты.Видимость = Ложь;
		Элементы.грпСтраницы.ПодчиненныеЭлементы.СтраницаОстаткиБезТС.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.ПодобранныеАгрегатыКоличество.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Функция СформироватьВозвращаемоеЗначение()
	
	мсвРезультат = Новый Массив;
	
	тзПодобранныеМатериалы = ПодобранныеАгрегаты.Выгрузить();
	тзПодобранныеМатериалы.Свернуть("Агрегат, МестоХранения","Количество");
	
	Для каждого текСтрока Из ПодобранныеАгрегаты Цикл
		Если ТипДокумента = Тип("ДокументСсылка.упПоступлениеАгрегатов") Тогда
			сткСоставСтроки = Новый Структура("Агрегат, МестоХранения, СтатьяРасходов, Количество");
		Иначе
			сткСоставСтроки = Новый Структура("Агрегат, МестоХранения, Количество");
		КонецЕсли;
		
	    ЗаполнитьЗначенияСвойств(сткСоставСтроки, текСтрока);
		мсвРезультат.Добавить(сткСоставСтроки);
	КонецЦикла; 
	
	Возврат мсвРезультат;
		
КонецФункции

#Область ОповещенияПоСобытиям

&НаКлиенте
Процедура ВводКоличестваЗавершение(ДополнительныеПараметры)
	
	Количество = ДополнительныеПараметры.Количество;
	Отказ = Ложь;
	Если Количество = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Количество не может быть равно нулю'");
		Отказ = Истина;
	ИначеЕсли Количество < 0 Тогда	
		ТекстСообщения = НСтр("ru = 'Количество не может быть отрицательным'");
		Отказ = Истина;
	КонецЕсли; 
	
	Если Истина
		И НЕ Отказ
		И ЗакрыватьПриВыбореАгрегата
	Тогда
		Закрыть(ДополнительныеПараметры.Агрегат);	
	КонецЕсли;
		
	Отбор = Новый Структура("Агрегат",ДополнительныеПараметры.Агрегат);		
	НайденныеСтроки = ПодобранныеАгрегаты.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() Тогда
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Введено несколько строк с выбранным агрегатом. %1'",СокрЛП(ДополнительныеПараметры.Агрегат)));	
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		ПоказатьПредупреждение(,ТекстСообщения);
	Иначе
		НоваяСтрока = ПодобранныеАгрегаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДополнительныеПараметры,, "Количество");
		НоваяСтрока.Количество = Количество;		
	КонецЕсли;
	
КонецПроцедуры // ВводКоличестваЗавершение()

&НаКлиенте
Процедура ПодобранныеАгрегатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрокаПодбора = ПодобранныеАгрегаты.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПодобранныеАгрегаты.Удалить(ТекущаяСтрокаПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДопОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	текСтрока = Элементы.СписокДопОборудование.ДанныеСтроки(ВыбраннаяСтрока);
	сткПараметры = Новый Структура("МестоХранения, Агрегат, Количество, Тип, ЕдиницаИзмерения, СтатьяРасходов");
	ЗаполнитьЗначенияСвойств(сткПараметры, текСтрока);
	сткПараметры.Количество = 1;
	сткПараметры.Агрегат = текСтрока.ДопОборудование;
	сткПараметры.МестоХранения = ПредопределенноеЗначение("Справочник.упСклады.ПустаяСсылка");
	
	ВводКоличестваЗавершение(сткПараметры);

КонецПроцедуры

#КонецОбласти 

#КонецОбласти