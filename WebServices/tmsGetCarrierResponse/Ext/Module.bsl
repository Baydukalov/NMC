
#Область ЗагрузкаСправочников
// Нет.
#КонецОбласти 

#Область ЗагрузкаДокументов
// Нет.
#КонецОбласти 

#Область ЗагрузкаЗаписейРегистров

// Нет.

#КонецОбласти 

#Область ВспомогательныеФункцииИПроцедуры

Функция GetCarrierResponse(CarrierResponse)
	
	текРезультат 		= Истина; 	 
		
	текПредложениеПеревозчикаXDTO	= CarrierResponse;
	
	Если ТипЗнч(текПредложениеПеревозчикаXDTO) = Тип("ОбъектXDTO") Тогда
				
		текЗаявкаНаТСУИД	= текПредложениеПеревозчикаXDTO.Получить("RequestUID");
		текПеревозчикУИД	= текПредложениеПеревозчикаXDTO.Получить("CarrierUID");
		текСоглашениеУИД	= текПредложениеПеревозчикаXDTO.Получить("AgreementUID");
		текТипТСУИД			= текПредложениеПеревозчикаXDTO.Получить("TypeVehicleUID");
		текТранспортноеСредствоУИД	= текПредложениеПеревозчикаXDTO.Получить("VehicleUID");
		текВремяПодачи		= текПредложениеПеревозчикаXDTO.Получить("BeginTime");
		текВремяЗавершения	= текПредложениеПеревозчикаXDTO.Получить("EndTime");
		текВодитель1УИД		= текПредложениеПеревозчикаXDTO.Получить("Driver1UID");
		текВодитель2УИД		= текПредложениеПеревозчикаXDTO.Получить("Driver2UID");
		текЭкспедиторУИД	= текПредложениеПеревозчикаXDTO.Получить("ForwarderUID");
		текВалютаУИД		= текПредложениеПеревозчикаXDTO.Получить("CurrencyUID");
		
		Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
			текВалюта		= Справочники.Валюты.ПолучитьСсылку(Новый УникальныйИдентификатор(текВалютаУИД));
		Иначе
			текВалюта 		= Справочники.Валюты.ПустаяСсылка();
		КонецЕсли;	
		текЗаявкаНаТС		= Документы.упЗаявкаНаТС.ПолучитьСсылку(Новый УникальныйИдентификатор(текЗаявкаНаТСУИД));
		текПеревозчик		= Справочники.упПартнеры.ПолучитьСсылку(Новый УникальныйИдентификатор(текПеревозчикУИД));
		текСоглашение	    = Справочники.упСоглашенияСПеревозчиками.ПолучитьСсылку(Новый УникальныйИдентификатор(текСоглашениеУИД));
		текТипТС			= Справочники.упТипыТС.ПолучитьСсылку(Новый УникальныйИдентификатор(текТипТСУИД));
				
		Если НЕ текВодитель1УИД = Неопределено И ЗначениеЗаполнено(текВодитель1УИД) Тогда
			текВодитель1	= Справочники.упСотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор(текВодитель1УИД));
		Иначе
			текВодитель1	= Справочники.упСотрудники.ПустаяСсылка();
		КонецЕсли;
		
		Если НЕ текВодитель2УИД = Неопределено И ЗначениеЗаполнено(текВодитель2УИД) Тогда
			текВодитель2	= Справочники.упСотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор(текВодитель2УИД));
		Иначе
			текВодитель2	= Справочники.упСотрудники.ПустаяСсылка();
		КонецЕсли;

		Если НЕ текЭкспедиторУИД = Неопределено И ЗначениеЗаполнено(текЭкспедиторУИД) Тогда
			текЭкспедитор	= Справочники.упСотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор(текЭкспедиторУИД));
		Иначе	
			текЭкспедитор	= Справочники.упСотрудники.ПустаяСсылка();
		КонецЕсли;
		
		Если НЕ текТранспортноеСредствоУИД = Неопределено И ЗначениеЗаполнено(текТранспортноеСредствоУИД) Тогда
			текТранспортноеСредство	= Справочники.упТранспортныеСредства.ПолучитьСсылку(Новый УникальныйИдентификатор(текТранспортноеСредствоУИД));
		Иначе	
			текТранспортноеСредство	= Справочники.упТранспортныеСредства.ПустаяСсылка();
		КонецЕсли;
				
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	упЗаявкаНаТС.Ссылка,
		|	упЗаявкаНаТС.ЧасовойПояс
		|ИЗ
		|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
		|ГДЕ
		|	упЗаявкаНаТС.Ссылка = &ЗаявкаНаТС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	упПартнеры.Ссылка
		|ИЗ
		|	Справочник.упПартнеры КАК упПартнеры
		|ГДЕ
		|	упПартнеры.Ссылка = &Перевозчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	упСоглашенияСПеревозчиками.Ссылка
		|ИЗ
		|	Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
		|ГДЕ
		|	упСоглашенияСПеревозчиками.Ссылка = &Соглашение
		|	И упСоглашенияСПеревозчиками.Владелец = &Перевозчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	упТипыТС.Ссылка
		|ИЗ
		|	Справочник.упТипыТС КАК упТипыТС
		|ГДЕ
		|	упТипыТС.Ссылка = &ТипТС";
	
		
		текЗапрос.УстановитьПараметр("ЗаявкаНаТС", 	текЗаявкаНаТС);
		текЗапрос.УстановитьПараметр("Перевозчик", 	текПеревозчик);
		текЗапрос.УстановитьПараметр("Соглашение", 	текСоглашение);
		текЗапрос.УстановитьПараметр("ТипТС", 		текТипТС);
		
		
		мсвРезультат	= текЗапрос.ВыполнитьПакет();
			
		// Заявка на ТС
		текВыборка = мсвРезультат[0].Выбрать();
		ЧасовойПоясЗаявкиНаТС = Справочники.упЧасовыеПояса.ПустаяСсылка();
		Если НЕ текВыборка.Следующий() Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти заявку  на транспортное средство с идентификатором ""%1""'"), текЗаявкаНаТСУИД);
			ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
			текРезультат = Ложь;
		Иначе
			ЧасовойПоясЗаявкиНаТС = текВыборка.ЧасовойПояс;
		КонецЕсли;
		
		// Перевозчик
		текВыборка = мсвРезультат[1].Выбрать();
		Если НЕ текВыборка.Следующий() Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти перевозчика с идентификатором ""%1""'"), текПеревозчикУИД);
			ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
			текРезультат = Ложь;
		КонецЕсли;
		
		// Соглашение
		текВыборка = мсвРезультат[2].Выбрать();
		Если НЕ текВыборка.Следующий() Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти соглашение с идентификатором ""%1""'"), текСоглашениеУИД);
			ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
			текРезультат = Ложь;
		КонецЕсли;
				
		// ТипТС
		текВыборка = мсвРезультат[3].Выбрать();
		Если НЕ текВыборка.Следующий() Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти тип транспортного средства с идентификатором ""%1""'"), текТипТСУИД);
			ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
			текРезультат = Ложь;
		КонецЕсли;
				
		// Транспортное средство
		Если ЗначениеЗаполнено(текТранспортноеСредство) Тогда
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			                  |	упТранспортныеСредства.Ссылка
			                  |ИЗ
			                  |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
			                  |ГДЕ
			                  |	упТранспортныеСредства.Ссылка = &Ссылка";
			текЗапрос.УстановитьПараметр("Ссылка", 	текТранспортноеСредство);
			текВыборка = текЗапрос.Выполнить().Выбрать();
			Если НЕ текВыборка.Следующий() Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти транспортное средство с идентификатором ""%1""'"), текТранспортноеСредствоУИД);
				ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
				текРезультат = Ложь;
			КонецЕсли;
		КонецЕсли;

				
		// Водитель 1
		Если ЗначениеЗаполнено(текВодитель1) Тогда
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			                  |	упСотрудники.Ссылка
			                  |ИЗ
			                  |	Справочник.упСотрудники КАК упСотрудники
			                  |ГДЕ
			                  |	упСотрудники.Ссылка = &Сотрудник";
			текЗапрос.УстановитьПараметр("Сотрудник", 	текВодитель1);
			текВыборка = текЗапрос.Выполнить().Выбрать();
			Если НЕ текВыборка.Следующий() Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти водителя1 с идентификатором ""%1""'"), текВодитель1УИД);
				ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
				текРезультат = Ложь;
			КонецЕсли;
		КонецЕсли;

		// Водитель 2
		Если ЗначениеЗаполнено(текВодитель2) Тогда
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			                  |	упСотрудники.Ссылка
			                  |ИЗ
			                  |	Справочник.упСотрудники КАК упСотрудники
			                  |ГДЕ
			                  |	упСотрудники.Ссылка = &Сотрудник";
			текЗапрос.УстановитьПараметр("Сотрудник", 	текВодитель2);
			текВыборка = текЗапрос.Выполнить().Выбрать();
			Если НЕ текВыборка.Следующий() Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти водителя2 с идентификатором ""%1""'"), текВодитель2УИД);
				ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
				текРезультат = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Экспедитор
		Если ЗначениеЗаполнено(текЭкспедитор) Тогда
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			                  |	упСотрудники.Ссылка
			                  |ИЗ
			                  |	Справочник.упСотрудники КАК упСотрудники
			                  |ГДЕ
			                  |	упСотрудники.Ссылка = &Сотрудник";
			текЗапрос.УстановитьПараметр("Сотрудник", 	текЭкспедитор);
			текВыборка = текЗапрос.Выполнить().Выбрать();
			Если НЕ текВыборка.Следующий() Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти водителя2 с идентификатором ""%1""'"), текЭкспедиторУИД);
				ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
				текРезультат = Ложь;
			КонецЕсли;
		КонецЕсли;
				
		// СтатьяРасходов
		
		текСписокСтатейРасходов = текПредложениеПеревозчикаXDTO.Costs;
		
		тбзСписокТоваров	= Новый ТаблицаЗначений;
		тбзСписокТоваров.Колонки.Добавить("СтатьяРасходов", 		Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
		тбзСписокТоваров.Колонки.Добавить("Сумма", 			Новый ОписаниеТипов("Число"));
		тбзСписокТоваров.Колонки.Добавить("СуммаУпрУчет", Новый ОписаниеТипов("Число"));
						
		Для й = 0 По текСписокСтатейРасходов.Количество() - 1  Цикл
			текСтрока	= текСписокСтатейРасходов.Получить(й);
			
			текСтатьяРасходовУИД	= текСтрока.CostUID;
			текСтатьяРасходов		= Справочники.упСтатьиДоходовРасходов.ПолучитьСсылку(Новый УникальныйИдентификатор(текСтатьяРасходовУИД));
			текСумма		= текСтрока.Sum;
			
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			                  |	упСтатьиДоходовРасходов.Ссылка
			                  |ИЗ
			                  |	Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
			                  |ГДЕ
			                  |	упСтатьиДоходовРасходов.Ссылка = &Ссылка";
			текЗапрос.УстановитьПараметр("Ссылка", текСтатьяРасходов);
			текВыборка 		= текЗапрос.Выполнить().Выбрать();
			Если НЕ текВыборка.Следующий() Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось найти статью расходов с идентификатором ""%1""'"), текСтатьяРасходовУИД);
				ЗаписьЖурналаРегистрации("GetCarrierResponse", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.tmsGetCarrierResponse,,ТекстОшибки);
				текРезультат = Ложь;
			Иначе
				текСтрокаСписка = тбзСписокТоваров.Добавить();
				текСтрокаСписка.СтатьяРасходов 	= текСтатьяРасходов;
				текСтрокаСписка.Сумма			= текСумма;
				Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
					текСтрокаСписка.СуммаУпрУчет 	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(текВалюта, текСтрокаСписка.Сумма) ;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если текРезультат Тогда
			
			текПредложениеДляПеревозчика			= Документы.упПредложениеПеревозчика.СоздатьДокумент();
			текПредложениеДляПеревозчика.Дата		= ТекущаяДата();
			текПредложениеДляПеревозчика.ЗаявкаНаТС	= текЗаявкаНаТС;
			текПредложениеДляПеревозчика.Перевозчик	= текПеревозчик;
			текПредложениеДляПеревозчика.Соглашение	= текСоглашение;
			текПредложениеДляПеревозчика.ТипТС		= текТипТС;
			текПредложениеДляПеревозчика.ВремяПодачи= текВремяПодачи;
			текПредложениеДляПеревозчика.Валюта		= текВалюта;
			текПредложениеДляПеревозчика.ВремяЗавершения = текВремяЗавершения; 
			
			текПредложениеДляПеревозчика.ТранспортноеСредство	= текТранспортноеСредство;
			
			текПредложениеДляПеревозчика.Водитель1		= текВодитель1;
			текПредложениеДляПеревозчика.Водитель2		= текВодитель2;
			текПредложениеДляПеревозчика.Экспедитор		= текЭкспедитор;
			
			Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
				текПредложениеДляПеревозчика.ЧасовойПояс = ЧасовойПоясЗаявкиНаТС;	
			КонецЕсли;  
		
			текПредложениеДляПеревозчика.ПлановыеРасходы.Загрузить(тбзСписокТоваров);
			
			Попытка
				текПредложениеДляПеревозчика.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПредложениеПеревозчика,, ТекстОшибки);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

#КонецОбласти 