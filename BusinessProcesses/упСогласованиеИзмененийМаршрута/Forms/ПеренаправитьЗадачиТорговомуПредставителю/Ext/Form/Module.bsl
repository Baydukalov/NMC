
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТекстЗаголовкаФормы = Параметры.ЗаголовокФормы;
	ЗаголовокПоУмолчанию = ПустаяСтрока(ТекстЗаголовкаФормы);
	Если НЕ ЗаголовокПоУмолчанию Тогда
		Заголовок = ТекстЗаголовкаФормы;
	КонецЕсли;
	
	ТекстЗаголовка = "";
	
	Если Параметры.КоличествоЗадач > 1 Тогда
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (%2)'"),
			?(ЗаголовокПоУмолчанию, НСтр("ru = 'Выбрано задач'"), ТекстЗаголовкаФормы),
			Строка(Параметры.КоличествоЗадач));
	ИначеЕсли Параметры.КоличествоЗадач = 1 Тогда
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"),
			?(ЗаголовокПоУмолчанию, НСтр("ru = 'Выбранная задача'"), ТекстЗаголовкаФормы),
			Строка(Параметры.Задача));
	Иначе
		Элементы.ДекорацияЗаголовок.Видимость = Ложь;
	КонецЕсли;
	Элементы.ДекорацияЗаголовок.Заголовок = ТекстЗаголовка;
	
	СобытиеМониторинга = Параметры.Задача.Предмет;
	Если ТипЗнч(СобытиеМониторинга) = Тип("ДокументСсылка.упСобытиеМониторинга") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упПартнерыТорговыеПредставители.ТорговыйПредставитель.Пользователь КАК Пользователь
		|ИЗ
		|	Документ.упСобытиеМониторинга КАК упСобытиеМониторинга
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упАдресаПартнеров КАК упАдресаПартнеров
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ТорговыеПредставители КАК упПартнерыТорговыеПредставители
		|			ПО упАдресаПартнеров.Партнер = упПартнерыТорговыеПредставители.Ссылка
		|		ПО упСобытиеМониторинга.АдресТочки = упАдресаПартнеров.Адрес
		|ГДЕ
		|	НЕ упПартнерыТорговыеПредставители.ТорговыйПредставитель.Пользователь = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		|	И упСобытиеМониторинга.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", СобытиеМониторинга);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЭтаФорма.Пользователи.Добавить(Выборка.Пользователь);	
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
		
	Элементы.ТорговыйПредставитель.СписокВыбора.ЗагрузитьЗначения(ЭтаФорма.Пользователи.ВыгрузитьЗначения());
	Если ЭтаФорма.Пользователи.Количество() = 1 Тогда
		ТорговыйПредставитель = ЭтаФорма.Пользователи[0].Значение;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(ТорговыйПредставитель) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан торговый представитель, которому следует перенаправить задачу.'"),,, "ТорговыйПредставитель", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Нет.

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OK(Команда)
	
	ОчиститьСообщения();
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	Закрыть(ПараметрыЗакрытия());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыЗакрытия()
	
	Результат = Новый Структура;
	Результат.Вставить("Исполнитель", ?(ЗначениеЗаполнено(ТорговыйПредставитель), ТорговыйПредставитель, Неопределено));
	Результат.Вставить("РольИсполнителя", ПредопределенноеЗначение("Справочник.РолиИсполнителей.ПустаяСсылка"));
	Результат.Вставить("ОсновнойОбъектАдресации", Неопределено);
	Результат.Вставить("ДополнительныйОбъектАдресации", Неопределено);
	Результат.Вставить("Комментарий", Комментарий);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
